000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. XRB0004.
000300 AUTHOR. TONY REY.
000400 INSTALLATION. TONY REY INC.
000500 DATE-WRITTEN. 11/05/91.
000600 DATE-COMPILED.
000700*================================================================*
000800* PROGRAM NARRATIVE
000900*================================================================*
001000* PRODUCE PROCEDURE LIBRARY CROSS REFERENCE FILE.
001100*================================================================*
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SOURCE-COMPUTER.  IBM-370.
001500 OBJECT-COMPUTER.  IBM-370.
001600 SPECIAL-NAMES. C01 IS TOP-OF-PAGE.
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900     SELECT IN-FILE            ASSIGN TO UT-S-INFILE
002000                               FILE STATUS IS IN-CC.
002100     SELECT XREF-FILE          ASSIGN TO UT-S-XREF
002200                               FILE STATUS IS XREF-CC.
002300     SELECT PRINT-FILE         ASSIGN TO UT-S-PRINT.
002400 DATA DIVISION.
002500 FILE SECTION.
002600*---------------------------------------------------------------*
002700 FD  IN-FILE
002800     BLOCK CONTAINS 0 RECORDS
002900     LABEL RECORDS ARE STANDARD.
003000 01  IN-REC              PIC X(81).
003100 01  IN-REC-R.
003200     05  FILLER          PIC X(01).
003300     05  IN-SL-SL        PIC X(02).
003400     05  IN-LINE.
003500         10 IN-ASTER     PIC X(01).
003600         10 FILLER       PIC X(69).
003700     05  IN-LINE-POS     REDEFINES IN-LINE
003800                         OCCURS 70 TIMES
003900                         PIC X(01).
004000     05  IN-SEQ          PIC X(08).
004100*---------------------------------------------------------------*
004200 FD  XREF-FILE
004300     BLOCK CONTAINS 0 RECORDS
004400     LABEL RECORDS ARE STANDARD.
004500 01  X-REC.
004600     05 X-PROC             PIC X(08).
004700     05 FILLER             PIC X(01).
004800     05 X-STEP             PIC X(08).
004900     05 FILLER             PIC X(01).
005000     05 X-PGM              PIC X(08).
005100     05 FILLER             PIC X(01).
005200     05 X-DDNAME           PIC X(08).
005300     05 FILLER             PIC X(01).
005400     05 X-DSN-OR-SYSOUT    PIC X(50).
005500     05 FILLER             PIC X(01).
005600     05 X-COMMENT          PIC X(70).
005700*---------------------------------------------------------------*
005800 FD  PRINT-FILE
005900     BLOCK CONTAINS 0 RECORDS
006000     LABEL RECORDS ARE STANDARD.
006100 01  PRINT-REC          PIC X(133).
006200 EJECT
006300*===============================================================*
006400 WORKING-STORAGE SECTION.
006500*===============================================================*
006600 77  IN-CC                 PIC XX VALUE SPACES.
006700 77  XREF-CC               PIC XX VALUE SPACES.
006800 COPY CCFILE.
006900*---------------------------------------------------------------*
007000 77  SW-PROCESS-SYMBOLIC   PIC X  VALUE 'N'.
007100 77  SW-EXEC-NAKED-Y-N     PIC X(1)         VALUE 'N'.
007200*---------------------------------------------------------------*
007300 77  AC-LEFT-PAREN         PIC 99           VALUE ZERO.
007400 77  AC-RIGHT-PAREN        PIC 99           VALUE ZERO.
007500 77  AC-LEFT-QUOTE         PIC 99           VALUE ZERO.
007600 77  AC-RIGHT-QUOTE        PIC 99           VALUE ZERO.
007700 01  AC-RECORDS.
007800     05  FILLER            PIC X(20)        VALUE
007900         'IN RECORDS: '.
008000     05  AC-IN             PIC 9(7)         VALUE ZERO.
008100     05  FILLER            PIC X(02)        VALUE SPACES.
008200     05  FILLER            PIC X(20)        VALUE
008300         'XREF RECORDS: '.
008400     05  AC-XREF           PIC 9(7)         VALUE ZERO.
008500*---------------------------------------------------------------*
008600 01  POS-TALLY.
008700     05  POS-SPACES        PIC 9(03)        VALUE ZERO.
008800     05  POS-PROC          PIC 9(03)        VALUE ZERO.
008900     05  POS-EXEC          PIC 9(03)        VALUE ZERO.
009000     05  POS-PGM           PIC 9(03)        VALUE ZERO.
009100     05  POS-DD            PIC 9(03)        VALUE ZERO.
009200     05  POS-DSN           PIC 9(03)        VALUE ZERO.
009300     05  POS-SYSOUT        PIC 9(03)        VALUE ZERO.
009400     05  POS-COMMENT       PIC 9(03)        VALUE ZERO.
009500*---------------------------------------------------------------*
009600* OUT TABLE WORK
009700*---------------------------------------------------------------*
009800 01  OUT-TBL.
009900     05  FILLER          PIC X(03).
010000     05  OUT-LINE        PIC X(70).
010100     05  OUT-LINE-POS    REDEFINES OUT-LINE
010200                         OCCURS 70 TIMES
010300                         PIC X(01).
010400     05  OUT-SEQ         PIC X(08).
010500***
010600 77  OUT-IDX             PIC 999 VALUE ZERO.
010700 77  OUT-IDX-MAX         PIC 999 VALUE 70.
010800***
010900 77  IN-IDX              PIC 999 VALUE ZERO.
011000 77  IN-IDX2             PIC 999 VALUE ZERO.
011100 77  IN-IDX-MAX          PIC 999 VALUE 70.
011200***
011300 77  FH-IN-IDX-GET-XXX   PIC 999 VALUE ZERO.
011400*---------------------------------------------------------------*
011500* COMPARE TABLE WORK
011600*---------------------------------------------------------------*
011700 01  COMPARE-TBL.
011800     05  COMPARE-ITEM    PIC X(70).
011900     05  COMPARE-POS     REDEFINES COMPARE-ITEM
012000                         OCCURS 70 TIMES
012100                         PIC X(01).
012200***
012300 77  COMPARE-LEN         PIC 99 VALUE ZERO.
012400 77  COMPARE-IDX         PIC 999 VALUE ZERO.
012500 77  COMPARE-IDX-MAX     PIC 999 VALUE 70.
012600***
012700 77  MATCH-IDX           PIC 999 VALUE ZERO.
012800*---------------------------------------------------------------*
012900 01  AC-LINE               PIC 9(03)        VALUE 99.
013000***
013100 01  HL.
013200     05  FILLER            PIC X(01)        VALUE SPACES.
013300     05  FILLER            PIC X(45)        VALUE SPACES.
013400     05  FILLER            PIC X(40)        VALUE
013500         'XRB0004 CROSS REFERENCE'.
013600     05  FILLER            PIC X(45)        VALUE SPACES.
013700***
013800 01  DL.
013900     05  FILLER            PIC X(01)        VALUE SPACES.
014000     05  DL-X              PIC X(132)       VALUE SPACES.
014100*---------------------------------------------------------------*
014200 01  RH-X-REC.
014300     05 RH-PROC            PIC X(08)        VALUE SPACES.
014400     05 FILLER             PIC X(01)        VALUE SPACES.
014500     05 RH-STEP            PIC X(08)        VALUE SPACES.
014600     05 FILLER             PIC X(01)        VALUE SPACES.
014700     05 RH-PGM             PIC X(08)        VALUE SPACES.
014800     05 FILLER             PIC X(01)        VALUE SPACES.
014900     05 RH-DDNAME          PIC X(08)        VALUE SPACES.
015000     05 FILLER             PIC X(01)        VALUE SPACES.
015100     05 RH-DSN-OR-SYSOUT   PIC X(50)        VALUE SPACES.
015200     05 FILLER             PIC X(01)        VALUE SPACES.
015300     05 RH-COMMENT         PIC X(70)        VALUE SPACES.
015400 EJECT
015500*===============================================================*
015600 PROCEDURE DIVISION.
015700*===============================================================*
015800 MAIN-PROGRAM.
015900 START-PROGRAM.
016000     OPEN INPUT IN-FILE.
016100     OPEN OUTPUT XREF-FILE
016200                 PRINT-FILE.
016300 MAIN-PROCESS.
016400     PERFORM 100-PARSE THRU
016500             100-PARSE-EXIT.
016600 FINISH-PROGRAM.
016700     MOVE AC-RECORDS TO DL-X.
016800     PERFORM 700-PRINT THRU
016900             700-PRINT-EXIT.
017000     CLOSE IN-FILE
017100           XREF-FILE
017200           PRINT-FILE.
017300     STOP RUN.
017400*---------------------------------------------------------------*
017500 100-PARSE.
017600 100-READ.
017700     READ IN-FILE.
017800     MOVE IN-CC TO CC-STATUS.
017900     IF CC-STATUS = CC-OK
018000         ADD 1 TO AC-IN
018100     ELSE
018200         IF CC-STATUS = CC-EOF
018300             GO TO 100-PARSE-EXIT
018400         ELSE
018500             DISPLAY 'ERROR,READ,IN,CC: ' CC-STATUS
018600             PERFORM 999-CANCEL THRU
018700                     999-CANCEL-EXIT.
018800 100-SL.
018900***  MAKE SURE ONLY VALID JCL STATEMENT IS PROCESSED *
019000     IF IN-SL-SL = '//' AND
019100        IN-ASTER NOT = '*'
019200         NEXT SENTENCE
019300     ELSE
019400         GO TO 100-PARSE.
019500 100-PROC.
019600     MOVE ' PROC ' TO COMPARE-ITEM.
019700     MOVE  6       TO COMPARE-LEN.
019800     PERFORM 150-FIND THRU
019900             150-FIND-EXIT.
020000     MOVE MATCH-IDX TO POS-PROC.
020100     IF POS-PROC > 2 AND POS-PROC < 23
020200***      EXEC NAKED PROCESS -
020300***      WRITE AN XREF RECORD FOR A PRIOR EXEC PGM THAT HAD NO
020400***      DSN/SYSOUT STATEMENTS (IE. SW-EXEC-NAKED-Y-N = 'Y') *
020500         PERFORM 450-WRITE-XREF THRU
020600                 450-WRITE-XREF-EXIT
020700         MOVE 'N' TO SW-EXEC-NAKED-Y-N
020800***      INIT PROCESS *
020900         MOVE SPACES   TO RH-X-REC
021000***      GET PROC NAME *
021100         MOVE 1 TO IN-IDX
021200         PERFORM 200-GET-MOVE THRU
021300                 200-GET-MOVE-EXIT
021400         MOVE OUT-LINE TO RH-PROC
021500***      PROCESS ANY SYMBOLICS OR COMMENT *
021600         MOVE 'Y'      TO SW-PROCESS-SYMBOLIC
021700         COMPUTE IN-IDX, POS-COMMENT = POS-PROC + 6
021800         PERFORM 225-COMMENT-OR-SYMBOLIC THRU
021900                 225-COMMENT-OR-SYMBOLIC-EXIT
022000***
022100         PERFORM 500-WRITE-XREF THRU
022200                 500-WRITE-XREF-EXIT
022300         GO TO 100-PARSE
022400     ELSE
022500         NEXT SENTENCE.
022600 100-EXEC-PGM.
022700     MOVE ' EXEC ' TO COMPARE-ITEM.
022800     MOVE  6       TO COMPARE-LEN.
022900     PERFORM 150-FIND THRU
023000             150-FIND-EXIT.
023100     MOVE MATCH-IDX TO POS-EXEC.
023200***
023300     MOVE ' PGM=' TO COMPARE-ITEM.
023400     MOVE  5       TO COMPARE-LEN.
023500     PERFORM 150-FIND THRU
023600             150-FIND-EXIT.
023700     MOVE MATCH-IDX TO POS-PGM.
023800***
023900     IF (POS-EXEC > 2 AND POS-EXEC < 23) AND
024000        (POS-PGM  > 7 AND POS-PGM  < 28)
024100***      EXEC NAKED PROCESS -
024200***      WRITE AN XREF RECORD FOR A PRIOR EXEC PGM THAT HAD NO
024300***      DSN/SYSOUT STATEMENTS (IE. SW-EXEC-NAKED-Y-N = 'Y') *
024400         PERFORM 450-WRITE-XREF THRU
024500                 450-WRITE-XREF-EXIT
024600         MOVE 'Y' TO SW-EXEC-NAKED-Y-N
024700***      INIT PROCESS *
024800         MOVE 'N'   TO SW-PROCESS-SYMBOLIC
024900         MOVE SPACE TO RH-DDNAME
025000         MOVE ZERO  TO POS-DD
025100***      GET PROGRAM NAME *
025200         COMPUTE IN-IDX = POS-PGM + 5
025300         PERFORM 200-GET-MOVE THRU
025400                 200-GET-MOVE-EXIT
025500         MOVE OUT-LINE   TO RH-PGM
025600***      GET PROC STEP *
025700         MOVE 1 TO IN-IDX
025800         PERFORM 200-GET-MOVE THRU
025900                 200-GET-MOVE-EXIT
026000         MOVE OUT-LINE TO RH-STEP
026100         GO TO 100-PARSE
026200     ELSE
026300         NEXT SENTENCE.
026400 100-PARSE-SYMBOLIC.
026500     IF SW-PROCESS-SYMBOLIC = 'Y'
026600***      THE FIRST EXEC PGM STATEMENT HAS NOT BEEN ENCOUNTERED *
026700         MOVE ZERO TO POS-SPACES
026800         INSPECT IN-LINE TALLYING POS-SPACES FOR LEADING SPACE
026900         COMPUTE IN-IDX = POS-SPACES + 1
027000         PERFORM 250-GET-COMMENT THRU
027100                 250-GET-COMMENT-EXIT
027200         MOVE OUT-LINE TO RH-COMMENT
027300         PERFORM 500-WRITE-XREF THRU
027400                 500-WRITE-XREF-EXIT
027500         GO TO 100-PARSE
027600     ELSE
027700         NEXT SENTENCE.
027800 100-DDNAME.
027900     IF IN-SL-SL = '//' AND
028000        IN-ASTER NOT = SPACE
028100         MOVE 1 TO IN-IDX
028200         PERFORM 200-GET-MOVE THRU
028300                 200-GET-MOVE-EXIT
028400         MOVE OUT-LINE TO RH-DDNAME
028500**       THERE MUST BE A ' DD ' IN THIS STATEMENT *
028600         MOVE ' DD ' TO COMPARE-ITEM
028700         MOVE  4     TO COMPARE-LEN
028800         PERFORM 150-FIND THRU
028900                 150-FIND-EXIT
029000         MOVE MATCH-IDX TO POS-DD
029100     ELSE
029200         NEXT SENTENCE.
029300 100-DD.
029400     IF POS-DD > ZERO
029500***      CONTINUE PROCESS FOR VALID DD'S ONLY *
029600         NEXT SENTENCE
029700     ELSE
029800         GO TO 100-PARSE.
029900 100-INIT-DSN-OR-SYSOUT.
030000 100-INIT-COMMENT.
030100     MOVE SPACE TO RH-DSN-OR-SYSOUT
030200                   RH-COMMENT.
030300 100-DSN.
030400     MOVE ' DSN=' TO COMPARE-ITEM.
030500     MOVE  5      TO COMPARE-LEN.
030600     PERFORM 150-FIND THRU
030700             150-FIND-EXIT.
030800     MOVE MATCH-IDX TO POS-DSN.
030900     IF POS-DSN > 2 AND
031000        POS-DSN < 45
031100         COMPUTE IN-IDX = POS-DSN + 5
031200         PERFORM 200-GET-MOVE THRU
031300                 200-GET-MOVE-EXIT
031400         MOVE OUT-LINE TO RH-DSN-OR-SYSOUT
031500***
031600         MOVE SPACE              TO COMPARE-ITEM
031700         MOVE  1                 TO COMPARE-LEN
031800         MOVE  FH-IN-IDX-GET-XXX TO IN-IDX
031900         PERFORM 150-FIND-EP2 THRU
032000                 150-FIND-EXIT
032100         MOVE MATCH-IDX TO POS-COMMENT
032200                           IN-IDX
032300         PERFORM 225-COMMENT-OR-SYMBOLIC THRU
032400                 225-COMMENT-OR-SYMBOLIC-EXIT
032500***
032600         MOVE 'N' TO  SW-EXEC-NAKED-Y-N
032700         PERFORM 500-WRITE-XREF THRU
032800                 500-WRITE-XREF-EXIT
032900     ELSE
033000         NEXT SENTENCE.
033100 100-SYSOUT.
033200     MOVE ' SYSOUT=' TO COMPARE-ITEM.
033300     MOVE  8         TO COMPARE-LEN.
033400     PERFORM 150-FIND THRU
033500             150-FIND-EXIT.
033600     COMPUTE POS-SYSOUT = MATCH-IDX + 1.
033700***
033800     IF POS-SYSOUT > 5 AND
033900        POS-SYSOUT < 48
034000         MOVE POS-SYSOUT TO IN-IDX
034100         PERFORM 200-GET-MOVE THRU
034200                 200-GET-MOVE-EXIT
034300         MOVE OUT-LINE           TO RH-DSN-OR-SYSOUT
034400***
034500         MOVE SPACE              TO COMPARE-ITEM
034600         MOVE  1                 TO COMPARE-LEN
034700         MOVE  FH-IN-IDX-GET-XXX TO IN-IDX
034800         PERFORM 150-FIND-EP2 THRU
034900                 150-FIND-EXIT
035000         MOVE MATCH-IDX TO POS-COMMENT
035100                           IN-IDX
035200         PERFORM 225-COMMENT-OR-SYMBOLIC THRU
035300                 225-COMMENT-OR-SYMBOLIC-EXIT
035400***
035500         MOVE 'N' TO  SW-EXEC-NAKED-Y-N
035600         PERFORM 500-WRITE-XREF THRU
035700                 500-WRITE-XREF-EXIT
035800     ELSE
035900         NEXT SENTENCE.
036000 100-REITERATE.
036100     GO TO 100-PARSE.
036200 100-PARSE-EXIT. EXIT.
036300*---------------------------------------------------------------*
036400* FIND COMPARE-ITEM WITHIN IN-LINE. IF MATCH, MATCH-IDX WILL
036500* POINT TO STARTING POSITION OF COMPARE-ITEM WITHIN IN-LINE.
036600*
036700* ENTRY POINTS TO THIS ROUTINE ARE 150-FIND, AND 150-FIND-EP2.
036800* IF A STARTING LOCATION IS KNOWN, THE '150-FIND-EP2' ENTRY
036900* IS USED.
037000*---------------------------------------------------------------*
037100 150-FIND.
037200     MOVE 1    TO IN-IDX.
037300 150-FIND-EP2.
037400     MOVE ZERO TO MATCH-IDX.
037500 150-LOOP.
037600     IF IN-LINE-POS (IN-IDX) = COMPARE-POS (1)
037700         PERFORM 175-FIND THRU
037800                 175-FIND-EXIT
037900         IF MATCH-IDX > ZERO
038000***          WE HAD A MATCH - EXIT *
038100             GO TO 150-FIND-EXIT
038200         ELSE
038300             NEXT SENTENCE
038400     ELSE
038500         NEXT SENTENCE.
038600 150-REITERATE.
038700     COMPUTE IN-IDX = IN-IDX + 1
038800     IF IN-IDX > IN-IDX-MAX
038900         NEXT SENTENCE
039000     ELSE
039100         GO TO 150-LOOP.
039200 150-FIND-EXIT. EXIT.
039300*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
039400 175-FIND.
039500 175-FIND-LEN-EQ-1.
039600     IF COMPARE-LEN = 1
039700***      WE HAVE A MATCH - LET'S MARK POSITION *
039800         MOVE IN-IDX TO MATCH-IDX
039900         GO TO 175-FIND-EXIT
040000     ELSE
040100         NEXT SENTENCE.
040200 175-FIND-LEN-GT-1.
040300     COMPUTE IN-IDX2 = IN-IDX + 1.
040400     MOVE 2 TO COMPARE-IDX.
040500 175-LOOP.
040600     IF IN-LINE-POS (IN-IDX2) = COMPARE-POS (COMPARE-IDX)
040700         IF COMPARE-IDX = COMPARE-LEN
040800***          WE HAVE A MATCH - LET'S MARK POSITION *
040900             MOVE IN-IDX TO MATCH-IDX
041000             GO TO 175-FIND-EXIT
041100         ELSE
041200             COMPUTE IN-IDX2     = IN-IDX2 + 1
041300             COMPUTE COMPARE-IDX = COMPARE-IDX + 1
041400             IF IN-IDX2 > IN-IDX-MAX
041500                 NEXT SENTENCE
041600             ELSE
041700                 GO TO 175-LOOP
041800     ELSE
041900         NEXT SENTENCE.
042000 175-FIND-EXIT. EXIT.
042100*---------------------------------------------------------------*
042200* GET AND MOVE AN ITEM
042300*---------------------------------------------------------------*
042400 200-GET-MOVE.
042500     MOVE 1      TO OUT-IDX.
042600     MOVE SPACES TO OUT-LINE.
042700     MOVE ZERO   TO AC-LEFT-PAREN
042800                    AC-RIGHT-PAREN.
042900     MOVE IN-IDX TO FH-IN-IDX-GET-XXX.
043000 200-LOOP.
043100 200-CHECK-IF-DONE.
043200     IF IN-IDX > IN-IDX-MAX
043300         GO TO 200-GET-MOVE-EXIT
043400     ELSE
043500         IF IN-LINE-POS(IN-IDX) = ' '
043600             GO TO 200-GET-MOVE-EXIT
043700         ELSE
043800             IF IN-LINE-POS(IN-IDX) = ',' AND
043900                AC-LEFT-PAREN       = AC-RIGHT-PAREN
044000                 GO TO 200-GET-MOVE-EXIT
044100             ELSE
044200                 NEXT SENTENCE.
044300 200-MOVE.
044400     MOVE IN-LINE-POS (IN-IDX) TO OUT-LINE-POS (OUT-IDX).
044500     MOVE IN-IDX               TO FH-IN-IDX-GET-XXX.
044600     IF IN-LINE-POS (IN-IDX) = '('
044700         COMPUTE AC-LEFT-PAREN = AC-LEFT-PAREN + 1
044800     ELSE
044900         IF IN-LINE-POS (IN-IDX) = ')'
045000             COMPUTE AC-RIGHT-PAREN = AC-RIGHT-PAREN + 1
045100         ELSE
045200             NEXT SENTENCE.
045300 200-INCREMENT.
045400     ADD 1 TO IN-IDX
045500              OUT-IDX.
045600 200-REITERATE.
045700     GO TO 200-LOOP.
045800 200-GET-MOVE-EXIT. EXIT.
045900*---------------------------------------------------------------*
046000* FIND COMMENT OR SYMBOLIC AND MOVE TO RH-COMMENT
046100* (LEADING BLANKS ARE STRIPPED OFF).
046200*
046300* NOTE: UPON ENTRY IN-IDX AND POS-COMMENT POINT TO THE START
046400* OF THE COMMENT/SYMBOLIC FIELD.  EXAMPLE:
046500*
046600*  DSN=SAMPLE,DISP=SHR    DUMB COMMENT
046700*                     |
046800*                     +--> IN-IDX AND POS-COMMENT
046900*---------------------------------------------------------------*
047000 225-COMMENT-OR-SYMBOLIC.
047100***  GET COMMENT OR SYMBOLICS *
047200     PERFORM 250-GET-COMMENT THRU
047300             250-GET-COMMENT-EXIT.
047400     MOVE OUT-LINE TO RH-COMMENT.
047500***  ELIMINATE LEADING SPACES FROM COMMENT *
047600     MOVE ZERO     TO POS-SPACES.
047700     INSPECT RH-COMMENT TALLYING POS-SPACES FOR LEADING SPACE.
047800     COMPUTE IN-IDX = POS-COMMENT + POS-SPACES.
047900     IF IN-IDX > IN-IDX-MAX
048000         MOVE SPACES TO RH-COMMENT
048100     ELSE
048200         PERFORM 250-GET-COMMENT THRU
048300                 250-GET-COMMENT-EXIT
048400         MOVE OUT-LINE TO RH-COMMENT.
048500 225-COMMENT-OR-SYMBOLIC-EXIT. EXIT.
048600*---------------------------------------------------------------*
048700* GET A COMMENT
048800*---------------------------------------------------------------*
048900 250-GET-COMMENT.
049000     MOVE 1      TO OUT-IDX.
049100     MOVE SPACES TO OUT-LINE.
049200     MOVE IN-IDX TO FH-IN-IDX-GET-XXX.
049300 250-MOVE.
049400     IF IN-IDX > IN-IDX-MAX
049500         GO TO 250-GET-COMMENT-EXIT
049600     ELSE
049700         MOVE IN-LINE-POS (IN-IDX) TO OUT-LINE-POS (OUT-IDX)
049800         MOVE IN-IDX               TO FH-IN-IDX-GET-XXX.
049900 250-INCREMENT.
050000     ADD 1 TO IN-IDX
050100              OUT-IDX.
050200 250-REITERATE.
050300     GO TO 250-MOVE.
050400 250-GET-COMMENT-EXIT. EXIT.
050500*---------------------------------------------------------------*
050600* WRITE CROSS REFERENCE FOR AN EXEC PGM WITH NO DSN/SYSOUT STMTS
050700*---------------------------------------------------------------*
050800 450-WRITE-XREF.
050900     IF SW-EXEC-NAKED-Y-N = 'Y'
051000         PERFORM 500-WRITE-XREF THRU
051100                 500-WRITE-XREF-EXIT
051200     ELSE
051300         NEXT SENTENCE.
051400 450-WRITE-XREF-EXIT. EXIT.
051500*---------------------------------------------------------------*
051600* WRITE CROSS REFERENCE FILE (XREF)
051700*---------------------------------------------------------------*
051800 500-WRITE-XREF.
051900     MOVE RH-X-REC TO X-REC.
052000     WRITE X-REC.
052100     MOVE XREF-CC TO CC-STATUS.
052200     IF CC-STATUS = CC-OK
052300         ADD 1 TO AC-XREF
052400     ELSE
052500         DISPLAY 'ERROR,WRITE,XREF,CC: ' CC-STATUS
052600         PERFORM 999-CANCEL THRU
052700                 999-CANCEL-EXIT.
052800 500-WRITE-XREF-EXIT. EXIT.
052900*---------------------------------------------------------------*
053000* PRINT REPORT
053100*---------------------------------------------------------------*
053200 700-PRINT.
053300     ADD 1 TO AC-LINE.
053400     IF AC-LINE > 60
053500         WRITE PRINT-REC FROM HL AFTER ADVANCING TOP-OF-PAGE
053600         WRITE PRINT-REC FROM DL AFTER ADVANCING 2 LINES
053700         MOVE 3 TO AC-LINE
053800     ELSE
053900         WRITE PRINT-REC FROM DL.
054000 700-PRINT-EXIT. EXIT.
054100*---------------------------------------------------------------*
054200* CANCEL ROUTINE
054300*---------------------------------------------------------------*
054400 999-CANCEL.
054500     DISPLAY 'XRB0004,ABNORMAL END * * * * * * * *'.
054600     MOVE +16 TO RETURN-CODE.
054700     STOP RUN.
054800 999-CANCEL-EXIT. EXIT.

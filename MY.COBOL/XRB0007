000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. XRB0007.
000300 AUTHOR. TONY REY.
000400 INSTALLATION. TONY REY INC.
000500 DATE-WRITTEN. 11/05/91.
000600 DATE-COMPILED.
000700*================================================================*
000800* PROGRAM NARRATIVE
000900*================================================================*
001000* PRODUCE THE SYMBOLICS OVERRIDE FILE FROM THE JOB XREF.
001100*================================================================*
001200* MODIFICATION LOG
001300*
001400* TWO PROGRAMMER INITIALS IMMEDIATELY FOLLOWED BY THE CHANGE
001500* DATE IN COLUMNS 73-80 SHOULD MARK ANY PROGRAM CHANGES.
001600*================================================================*
001700* MM/DD/YY  PROGRAMMER  PROBLEM/RFS NUMBER: DESCRIPTION
001800* --------  ----------  ------------------------------------------
001900* 08/12/92  TONY REY    I WROTE THIS PROGRAM.
002000*================================================================*
002100 ENVIRONMENT DIVISION.
002200 CONFIGURATION SECTION.
002300 SOURCE-COMPUTER.  IBM-370.
002400 OBJECT-COMPUTER.  IBM-370.
002500 SPECIAL-NAMES. C01 IS TOP-OF-PAGE.
002600 INPUT-OUTPUT SECTION.
002700 FILE-CONTROL.
002800     SELECT XREF-JOB             ASSIGN TO UT-S-XR0071I
002900                                 FILE STATUS IS XREF-JOB-CC.
003000     SELECT SYMBOLICS            ASSIGN TO DA-XR0071O
003100                                 ORGANIZATION IS INDEXED
003200                                 ACCESS IS DYNAMIC
003300                                 RECORD KEY  IS SYM-KEY
003400                                 FILE STATUS IS SYMBOLICS-CC.
003500 DATA DIVISION.
003600 FILE SECTION.
003700*---------------------------------------------------------------*
003800 FD  XREF-JOB
003900     BLOCK CONTAINS 0 RECORDS
004000     RECORDING MODE IS F
004100     LABEL RECORDS ARE STANDARD.
004200 01  XREF-JOB-REC.
004300     05 X-JOB                  PIC X(08).
004400     05 FILLER                 PIC X(01).
004500     05 X-JOB-STEP             PIC X(08).
004600     05 FILLER                 PIC X(01).
004700     05 X-JOB-PROC-PGM-FLAG    PIC X(04).
004800     05 FILLER                 PIC X(01).
004900     05 X-JOB-PROC-PGM         PIC X(08).
005000     05 FILLER                 PIC X(01).
005100     05 X-JOB-DDNAME           PIC X(17).
005200     05 FILLER                 PIC X(01).
005300     05 X-JOB-DSN-OR-SYSOUT    PIC X(50).
005400     05 FILLER                 PIC X(01).
005500     05 X-JOB-COMMENT          PIC X(70).
005600*---------------------------------------------------------------*
005700 FD  SYMBOLICS
005800     DATA RECORD IS SYM-REC.
005900 01  SYM-REC.
006000     05 SYM-KEY.
006100        10 SYM-JOB              PIC X(08).
006200        10 SYM-STEP             PIC X(08).
006300        10 SYM-PROC             PIC X(08).
006400        10 SYM-SYMBOLIC         PIC X(07).
006500     05 SYM-VALUE               PIC X(40).
006600 EJECT
006700*===============================================================*
006800 WORKING-STORAGE SECTION.
006900*===============================================================*
007000 COPY CCFILE.
007100 77  XREF-JOB-CC           PIC XX VALUE SPACES.
007200 77  SYMBOLICS-CC          PIC XX VALUE SPACES.
007300*---------------------------------------------------------------*
007400 77  AC-LEFT-QUOTE         PIC 99           VALUE ZERO.
007500 77  AC-RIGHT-QUOTE        PIC 99           VALUE ZERO.
007600*---------------------------------------------------------------*
007700 77  AC-XREF-JOB           PIC 9(7)         VALUE ZERO.
007800 77  AC-SYMBOLICS          PIC 9(7)         VALUE ZERO.
007900*---------------------------------------------------------------*
008000 77  CSN-ZERO              PIC 9            VALUE ZERO.
008100 77  CSA-PROC              PIC X(4)         VALUE 'PROC'.
008200 77  CSA-COMMA             PIC X            VALUE ','.
008300 77  CSA-EQUAL             PIC X            VALUE '='.
008400 01  CSA-QUOTE-VIA-BRUTE-FORCE.
008500     05 CSN-125            PIC S9(4) COMP   VALUE +125.
008600     05 CSN-125-R          REDEFINES CSN-125.
008700        10 FILLER          PIC X.
008800        10 CSA-QUOTE       PIC X.
008900*---------------------------------------------------------------*
009000 77  POS-SCAN              PIC 9(03)        VALUE ZERO.
009100 77  POS-ANCHOR            PIC 9(03)        VALUE ZERO.
009200 77  SCAN-SIZE             PIC 9(03)        VALUE ZERO.
009300 77  WS-PHASE              PIC 9            VALUE ZERO.
009400 77  SYM-LEN               PIC 9(03)        VALUE ZERO.
009500*---------------------------------------------------------------*
009600 EJECT
009700*===============================================================*
009800 PROCEDURE DIVISION.
009900*===============================================================*
010000 MAIN-PROGRAM.
010100     DISPLAY 'XRB0007 STARTING'.
010200 OPEN-FILES.
010300     DISPLAY 'OPENING FILES'.
010400***
010500     OPEN INPUT XREF-JOB.
010600     MOVE XREF-JOB-CC TO CC-STATUS.
010700     IF CC-STATUS NOT = CC-OK
010800         DISPLAY 'ERROR,OPEN,XREF-JOB,CC: ' CC-STATUS
010900         PERFORM 999-CANCEL THRU
011000                 999-CANCEL-EXIT
011100     END-IF.
011200***
011300     OPEN OUTPUT SYMBOLICS.
011400     MOVE SYMBOLICS-CC TO CC-STATUS.
011500     IF CC-STATUS NOT = CC-OK
011600         DISPLAY 'ERROR,OPEN,SYMBOLICS,CC: ' CC-STATUS
011700         PERFORM 999-CANCEL THRU
011800                 999-CANCEL-EXIT
011900     END-IF.
012000 INIT-READ.
012100     DISPLAY 'INITIAL READ'.
012200     PERFORM 400-READ THRU
012300             400-READ-EXIT.
012400 MAIN-PROCESS.
012500     DISPLAY 'MAIN PROCESS'.
012600     PERFORM 100-MAIN-PROCESS THRU
012700             100-MAIN-PROCESS-EXIT
012800             UNTIL XREF-JOB-CC = CC-EOF.
012900 CLOSE-FILES.
013000     DISPLAY 'CLOSE FILES'.
013100***
013200     CLOSE XREF-JOB.
013300     MOVE XREF-JOB-CC TO CC-STATUS.
013400     IF CC-STATUS NOT = CC-OK
013500         DISPLAY 'ERROR,CLOSE,XREF-JOB,CC: ' CC-STATUS
013600         PERFORM 999-CANCEL THRU
013700                 999-CANCEL-EXIT
013800     END-IF.
013900***
014000     CLOSE SYMBOLICS.
014100     MOVE SYMBOLICS-CC TO CC-STATUS.
014200     IF CC-STATUS NOT = CC-OK
014300         DISPLAY 'ERROR,CLOSE,SYMBOLICS,CC: ' CC-STATUS
014400         PERFORM 999-CANCEL THRU
014500                 999-CANCEL-EXIT
014600     END-IF.
014700 100-FINISH-PROGRAM.
014800     PERFORM 900-STATISTICS THRU
014900             900-STATISTICS-EXIT.
015000     DISPLAY 'XRB0007 ENDING'.
015100     STOP RUN.
015200*---------------------------------------------------------------*
015300 100-MAIN-PROCESS.
015400     IF X-JOB-PROC-PGM-FLAG   = CSA-PROC AND
015500        X-JOB-DDNAME          = SPACES   AND
015600        X-JOB-DSN-OR-SYSOUT   = SPACES   AND
015700        X-JOB-COMMENT     NOT = SPACES
015800***      START PROCESSING THE SYBOLIC OVERRIDES *
015900         PERFORM 110-PARSE THRU
016000                 110-PARSE-EXIT
016100     END-IF.
016200 100-READ.
016300     PERFORM 400-READ THRU
016400             400-READ-EXIT.
016500 100-MAIN-PROCESS-EXIT. EXIT.
016600*---------------------------------------------------------------*
016700* PARSE THE COMMENT FIELD FOR SYMBOLICS
016800*---------------------------------------------------------------*
016900 110-PARSE.
017000     MOVE ZERO                     TO WS-PHASE.
017100     MOVE 1                        TO POS-ANCHOR.
017200     MOVE 1                        TO POS-SCAN.
017300     MOVE LENGTH OF X-JOB-COMMENT  TO SCAN-SIZE.
017400 110-PARSE-LOOP.
017500     IF WS-PHASE = ZERO
017600         IF X-JOB-COMMENT(POS-SCAN: 1) = SPACE
017700             GO TO 110-PARSE-EXIT
017800         ELSE
017900             MOVE 1              TO WS-PHASE
018000             MOVE POS-SCAN       TO POS-ANCHOR
018100             MOVE ZERO           TO AC-LEFT-QUOTE
018200             MOVE ZERO           TO AC-RIGHT-QUOTE
018300             MOVE SPACES         TO SYM-REC
018400             MOVE X-JOB          TO SYM-JOB
018500             MOVE X-JOB-STEP     TO SYM-STEP
018600             MOVE X-JOB-PROC-PGM TO SYM-PROC
018700             GO TO 110-PHASE-1
018800         END-IF
018900     END-IF.
019000 110-PHASE-2.
019100     IF WS-PHASE = 2
019200         IF X-JOB-COMMENT(POS-SCAN: 1) = SPACE OR
019300            X-JOB-COMMENT(POS-SCAN: 1) = CSA-COMMA
019400             MOVE SPACES TO SYM-VALUE
019500             PERFORM 500-WRITE-SYMBOLICS THRU
019600                     500-WRITE-SYMBOLICS-EXIT
019700             IF X-JOB-COMMENT(POS-SCAN: 1) = SPACE
019800                 GO TO 110-PARSE-EXIT
019900             ELSE
020000                 MOVE ZERO TO WS-PHASE
020100                 GO TO 110-CONTINUE
020200             END-IF
020300         ELSE
020400             MOVE 3 TO WS-PHASE
020500             IF X-JOB-COMMENT (POS-SCAN: 1) = CSA-QUOTE
020600                 ADD 1 TO AC-LEFT-QUOTE
020700                 COMPUTE POS-ANCHOR = POS-SCAN + 1
020800                 GO TO 110-CONTINUE
020900             ELSE
021000                 MOVE POS-SCAN TO POS-ANCHOR
021100                 GO TO 110-PHASE-3
021200             END-IF
021300         END-IF
021400     END-IF.
021500 110-PHASE-4.
021600     IF WS-PHASE = 4
021700         IF X-JOB-COMMENT(POS-SCAN: 1) = SPACE
021800             GO TO 110-PARSE-EXIT
021900         ELSE
022000             IF X-JOB-COMMENT(POS-SCAN: 1) = CSA-COMMA
022100                 MOVE ZERO TO WS-PHASE
022200                 GO TO 110-CONTINUE
022300             ELSE
022400                 GO TO 110-PARSE-ERROR
022500             END-IF
022600         END-IF
022700     END-IF.
022800 110-PHASE-1.
022900     IF WS-PHASE = 1
023000         IF X-JOB-COMMENT (POS-SCAN: 1) = CSA-EQUAL
023100             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
023200             IF SYM-LEN > ZERO AND
023300                SYM-LEN < 8
023400                 MOVE X-JOB-COMMENT (POS-ANCHOR: SYM-LEN)
023500                   TO SYM-SYMBOLIC
023600                 MOVE 2 TO WS-PHASE
023700                 GO TO 110-CONTINUE
023800             ELSE
023900                 GO TO 110-PARSE-ERROR
024000             END-IF
024100         END-IF
024200     END-IF.
024300 110-PHASE-3.
024400     IF WS-PHASE = 3
024500         IF X-JOB-COMMENT(POS-SCAN: 1) = CSA-QUOTE
024600             COMPUTE AC-RIGHT-QUOTE = AC-RIGHT-QUOTE + 1
024700         END-IF
024800***
024900         IF X-JOB-COMMENT (POS-SCAN: 1) = SPACE AND
025000            AC-LEFT-QUOTE               = AC-RIGHT-QUOTE
025100             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
025200             IF SYM-LEN = ZERO
025300                 MOVE SPACES TO SYM-VALUE
025400             ELSE
025500                 MOVE X-JOB-COMMENT (POS-ANCHOR: SYM-LEN)
025600                   TO SYM-VALUE
025700             END-IF
025800             PERFORM 500-WRITE-SYMBOLICS THRU
025900                     500-WRITE-SYMBOLICS-EXIT
026000             GO TO 110-PARSE-EXIT
026100         END-IF
026200***
026300         IF X-JOB-COMMENT(POS-SCAN: 1) = CSA-QUOTE AND
026400            AC-LEFT-QUOTE              = AC-RIGHT-QUOTE
026500             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
026600             IF SYM-LEN = ZERO
026700                 MOVE SPACES TO SYM-VALUE
026800             ELSE
026900                 MOVE X-JOB-COMMENT (POS-ANCHOR: SYM-LEN)
027000                   TO SYM-VALUE
027100             END-IF
027200             PERFORM 500-WRITE-SYMBOLICS THRU
027300                     500-WRITE-SYMBOLICS-EXIT
027400             MOVE 4 TO WS-PHASE
027500         END-IF
027600***
027700         IF X-JOB-COMMENT(POS-SCAN: 1) = CSA-COMMA AND
027800            AC-LEFT-QUOTE              = AC-RIGHT-QUOTE
027900             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
028000             IF SYM-LEN = ZERO
028100                 MOVE SPACES TO SYM-VALUE
028200             ELSE
028300                 MOVE X-JOB-COMMENT (POS-ANCHOR: SYM-LEN)
028400                   TO SYM-VALUE
028500             END-IF
028600             PERFORM 500-WRITE-SYMBOLICS THRU
028700                     500-WRITE-SYMBOLICS-EXIT
028800             MOVE ZERO TO WS-PHASE
028900         END-IF
029000     END-IF.
029100 110-CONTINUE.
029200     IF POS-SCAN < SCAN-SIZE
029300         COMPUTE POS-SCAN = POS-SCAN + 1
029400         GO TO 110-PARSE-LOOP
029500     ELSE
029600         GO TO 110-PARSE-EXIT
029700     END-IF.
029800 110-PARSE-ERROR.
029900     DISPLAY 'ERROR,DATA,XREF-JOB,REC: ' AC-XREF-JOB.
030000     DISPLAY '->' XREF-JOB-REC.
030100 110-PARSE-EXIT. EXIT.
030200*---------------------------------------------------------------*
030300* READ THE PROCEDURE XREF
030400*---------------------------------------------------------------*
030500 400-READ.
030600     READ XREF-JOB.
030700     MOVE XREF-JOB-CC TO CC-STATUS.
030800     IF CC-STATUS = CC-OK
030900         ADD 1 TO AC-XREF-JOB
031000     ELSE
031100         IF CC-STATUS = CC-EOF
031200             GO TO 400-READ-EXIT
031300         ELSE
031400             DISPLAY 'ERROR,READ,XREF-JOB,CC: ' CC-STATUS
031500             PERFORM 999-CANCEL THRU
031600                     999-CANCEL-EXIT
031700         END-IF
031800     END-IF.
031900 400-READ-EXIT. EXIT.
032000*---------------------------------------------------------------*
032100* WRITE SYMBOLICS
032200*---------------------------------------------------------------*
032300 500-WRITE-SYMBOLICS.
032400     WRITE SYM-REC.
032500     MOVE SYMBOLICS-CC TO CC-STATUS.
032600     IF CC-STATUS = CC-OK
032700         ADD 1 TO AC-SYMBOLICS
032800     ELSE
032900         IF CC-STATUS = CC-DUP-KEY
033000             DISPLAY 'ERROR,DATA,XREF-JOB,REC: ' AC-XREF-JOB
033100             DISPLAY 'ERROR,DUP KEY,SYM-REC: '  SYM-REC
033200         ELSE
033300             DISPLAY 'ERROR,WRITE,SYMBOLICS,CC: ' CC-STATUS
033400             PERFORM 999-CANCEL THRU
033500                     999-CANCEL-EXIT
033600         END-IF
033700     END-IF.
033800 500-WRITE-SYMBOLICS-EXIT. EXIT.
033900*---------------------------------------------------------------*
034000* PRINT REPORT
034100*---------------------------------------------------------------*
034200 900-STATISTICS.
034300     DISPLAY 'SYMBOLICS WRITTEN: ' AC-SYMBOLICS.
034400     DISPLAY 'XREF-JOB READ    : ' AC-XREF-JOB.
034500 900-STATISTICS-EXIT. EXIT.
034600*---------------------------------------------------------------*
034700* CANCEL ROUTINE
034800*---------------------------------------------------------------*
034900 999-CANCEL.
035000     DISPLAY 'XRB0007,ABNORMAL END * * * * * * * *'.
035100     PERFORM 900-STATISTICS THRU
035200             900-STATISTICS-EXIT.
035300     COMPUTE CSN-ZERO = CSN-ZERO / CSN-ZERO.
035400 999-CANCEL-EXIT. EXIT.

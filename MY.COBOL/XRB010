000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. XRB010.
000300 AUTHOR. TONY REY.
000400 INSTALLATION. TONY REY INC.
000500 DATE-WRITTEN. 11/05/91.
000600 DATE-COMPILED.
000700*================================================================*
000800* PROGRAM NARRATIVE
000900*================================================================*
001000* READ THE CROSS REFERENCE FILES, AND THE REPORT FILE IN ORDER
001100* TO PRODUCE THE DOCUMENTATION RECORDS.
001200*================================================================*
001300 ENVIRONMENT DIVISION.
001400 CONFIGURATION SECTION.
001500 SOURCE-COMPUTER.  IBM-370.
001600 OBJECT-COMPUTER.  IBM-370.
001700 SPECIAL-NAMES. C01 IS TOP-OF-PAGE.
001800 INPUT-OUTPUT SECTION.
001900 FILE-CONTROL.
002000     SELECT PARM-FILE          ASSIGN TO UT-S-XR01001I
002100                               FILE STATUS IS CC-PARM.
002200     SELECT XREF-FILE          ASSIGN TO UT-S-XR01002I
002300                               FILE STATUS IS CC-XREF.
002400     SELECT PSYM-FILE          ASSIGN TO DA-XR01003I
002500                               ORGANIZATION IS INDEXED
002600                               ACCESS IS RANDOM
002700                               RECORD KEY  IS PSYM-KEY
002800                               FILE STATUS IS CC-PSYM.
002900     SELECT RPT-FILE           ASSIGN TO DA-XR01004I
003000                               ORGANIZATION IS INDEXED
003100                               ACCESS IS SEQUENTIAL
003200                               RECORD KEY  IS R-KEY
003300                               FILE STATUS IS CC-RPT.
003400     SELECT DOC-FILE           ASSIGN TO UT-S-XR01001O
003500                               FILE STATUS IS CC-DOC.
003600 DATA DIVISION.
003700 FILE SECTION.
003800 EJECT
003900*---------------------------------------------------------------*
004000 FD  PARM-FILE
004100*---------------------------------------------------------------*
004200     BLOCK CONTAINS 0 RECORDS
004300     RECORDING MODE IS F
004400     LABEL RECORDS ARE STANDARD
004500     DATA RECORD IS PARM-REC.
004600 01  PARM-REC.
004700     05 P-TYPE           PIC X(09).
004800        88 P-JOB         VALUE 'JOB      '.
004900        88 P-JOB-STEP    VALUE 'JOBSTEP  '.
005000        88 P-JOB-DESC    VALUE 'JOBDESC  '.
005100        88 P-PROC        VALUE 'PROC     '.
005200        88 P-SYS         VALUE 'SYS      '.
005300        88 P-SYS-SUB     VALUE 'SUBSYS   '.
005400        88 P-USER        VALUE 'USER     '.
005500        88 P-EXT         VALUE 'EXT      '.
005600        88 P-DEPT        VALUE 'DEPT     '.
005700        88 P-DSN-DESC    VALUE 'DSN DESC '.
005800        88 P-RPT-DESC    VALUE 'RPT DESC '.
005900     05 P-DATA           PIC X(71).
006000 EJECT
006100*---------------------------------------------------------------*
006200 FD  XREF-FILE
006300*---------------------------------------------------------------*
006400     BLOCK CONTAINS 0 RECORDS
006500     RECORDING MODE IS F
006600     LABEL RECORDS ARE STANDARD
006700     DATA RECORD IS XREF-REC.
006800 01  XREF-REC.
006900     05 X-PROC             PIC X(08).
007000     05 FILLER             PIC X(01).
007100     05 X-PROC-STEP        PIC X(08).
007200     05 FILLER             PIC X(01).
007300     05 X-PGM              PIC X(08).
007400     05 FILLER             PIC X(01).
007500     05 X-DDNAME           PIC X(08).
007600     05 FILLER             PIC X(01).
007700     05 X-DSN-OR-SYSOUT    PIC X(50).
007800     05 FILLER             PIC X(01).
007900     05 X-COMMENT          PIC X(70).
008000 EJECT
008100*---------------------------------------------------------------*
008200 FD  PSYM-FILE
008300*---------------------------------------------------------------*
008400     DATA RECORD IS PSYM-REC.
008500 01  PSYM-REC.
008600     05 PSYM-KEY.
008700        10 PSYM-PROC        PIC X(08).
008800        10 PSYM-SYMBOLIC    PIC X(07).
008900     05 PSYM-VALUE          PIC X(40).
009000 EJECT
009100*---------------------------------------------------------------*
009200 FD  RPT-FILE
009300*---------------------------------------------------------------*
009400     DATA RECORD IS RPT-REC.
009500 01  RPT-REC.
009600     05 R-KEY.
009700        10 R-JOB            PIC X(08).
009800        10 R-DEPT           PIC X(04).
009900        10 R-SEQ            PIC X(02).
010000     05 R-NO                PIC X(08).
010100     05 R-TITLE             PIC X(22).
010200     05 R-CLASS             PIC X(01).
010300     05 FILLER              PIC X(01).
010400     05 R-COPIES            PIC X(01).
010500     05 R-USER              PIC X(09).
010600     05 R-EXT               PIC X(03).
010700     05 R-COMMENT           PIC X(14).
010800     05 R-LOCATION          PIC X(01).
010900        88 LAKELAND         VALUE 'L'.
011000        88 ATLANTA          VALUE 'A'.
011100     05 FILLER              PIC X(06).
011200 EJECT
011300*---------------------------------------------------------------*
011400 FD  DOC-FILE
011500*---------------------------------------------------------------*
011600     BLOCK CONTAINS 0 RECORDS
011700     RECORDING MODE IS F
011800     LABEL RECORDS ARE STANDARD
011900     DATA RECORD IS DOC-REC.
012000 01  DOC-REC.
012100     05 D-JOB              PIC X(08).
012200     05 D-JOB-STEP         PIC X(08).
012300     05 D-PROC             PIC X(08).
012400     05 D-SORT-01          PIC X(1).
012500        88 D-DATASET-SW    VALUE SPACE.
012600        88 D-REPORT-SW     VALUE 'R'.
012700     05 D-PROC-STEP        PIC X(08).
012800     05 D-SORT-02          PIC X(1).
012900        88 D-INPUT-DATA-SW                 VALUE 'I'.
013000        88 D-OUTPUT-DATA-SW                VALUE 'O'.
013100        88 D-REPORT-DATA-SW                VALUE SPACE.
013200     05 D-PGM              PIC X(08).
013300     05 D-DDNAME           PIC X(08).
013400     05 D-DSN              PIC X(50).
013500     05 D-RPT-DATA.
013600        10 D-RPT-NO        PIC X(08).
013700        10 D-RPT-TITLE     PIC X(22).
013800        10 D-RPT-DEPT      PIC X(04).
013900        10 D-RPT-CLASS     PIC X(01).
014000        10 D-RPT-COPIES    PIC X(01).
014100        10 D-RPT-USER      PIC X(09).
014200        10 D-RPT-EXT       PIC X(03).
014300        10 D-RPT-LOCATION  PIC X(01).
014400     05 D-COMMENT          PIC X(70).
014500     05 D-SYS              PIC X(20).
014600     05 D-SYS-SUB          PIC X(20).
014700 EJECT
014800*===============================================================*
014900 WORKING-STORAGE SECTION.
015000*===============================================================*
015100 77  CC-PARM               PIC XX VALUE SPACES.
015200 77  CC-XREF               PIC XX VALUE SPACES.
015300 77  CC-PSYM               PIC XX VALUE SPACES.
015400 77  CC-RPT                PIC XX VALUE SPACES.
015500 77  CC-DOC                PIC XX VALUE SPACES.
015600 COPY CCFILE.
015700 77  AC-PARM               PIC 9(7)         VALUE ZERO.
015800 77  AC-XREF               PIC 9(7)         VALUE ZERO.
015900 77  AC-PSYM               PIC 9(7)         VALUE ZERO.
016000 77  AC-RPT                PIC 9(7)         VALUE ZERO.
016100 77  AC-DOC                PIC 9(7)         VALUE ZERO.
016200*---------------------------------------------------------------*
016300* FIELD HOLD PARAMETERS
016400*---------------------------------------------------------------*
016500 77  FH-P-JOB              PIC X(08) VALUE SPACES.
016600 77  FH-P-JOB-STEP         PIC X(08) VALUE SPACES.
016700 77  FH-P-JOB-DESC         PIC X(50) VALUE SPACES.
016800 77  FH-P-PROC             PIC X(08) VALUE SPACES.
016900 77  FH-P-SYS              PIC X(20) VALUE SPACES.
017000 77  FH-P-SYS-SUB          PIC X(20) VALUE SPACES.
017100 77  FH-P-USER             PIC X(09) VALUE SPACES.
017200 77  FH-P-EXT              PIC X(03) VALUE SPACES.
017300 77  FH-P-DEPT             PIC X(04) VALUE SPACES.
017400 77  FH-P-DSN-DESC         PIC X(50) VALUE SPACES.
017500 77  FH-P-RPT-DESC         PIC X(22) VALUE SPACES.
017600*---------------------------------------------------------------*
017700* WORK REPORT TABLE
017800*---------------------------------------------------------------*
017900 01  REPORT-TBL.
018000     05  RT-ENTRIES       OCCURS 70 TIMES.
018100         10 RT-ENTRY.
018200            15 RT-KEY.
018300               20 RT-JOB    PIC X(08).
018400               20 RT-DEPT   PIC X(04).
018500               20 RT-SEQ    PIC X(02).
018600            15 RT-NO        PIC X(08).
018700            15 RT-TITLE     PIC X(22).
018800            15 RT-CLASS     PIC X(01).
018900            15 FILLER       PIC X(01).
019000            15 RT-COPIES    PIC X(01).
019100            15 RT-USER      PIC X(09).
019200            15 RT-EXT       PIC X(03).
019300            15 RT-COMMENT   PIC X(14).
019400            15 RT-LOCATION  PIC X(01).
019500            15 FILLER       PIC X(06).
019600         10 RT-USED         PIC X(01).
019700***
019800 77  REPORT-IDX             PIC 999 VALUE ZERO.
019900 77  REPORT-TBL-SIZE        PIC 999 VALUE ZERO.
020000 77  REPORT-IDX-MAX         PIC 999 VALUE 70.
020100*---------------------------------------------------------------*
020200 77  WS-POS               PIC 9(03).
020300 77  WS-POS-OFFSET        PIC 9(03).
020400 77  WS-POS-RS            PIC 9(03).
020500 77  WS-LEN               PIC 9(03).
020600 77  WS-LEN-SYM           PIC 9(03).
020700 77  WS-IDX               PIC 9(03).
020800 77  WS-SYMBOLIC          PIC X(07).
020900 77  SW-FINI              PIC X(01).
021000 77  SW-FOUND             PIC X(01).
021100 77  WS-UNRESOLVED        PIC X(50).
021200 77  WS-RESOLVED          PIC X(50).
021300 77  WS-DSN-OR-SYSOUT     PIC X(50).
021400 77  AC-SPACES            PIC 9(7)  VALUE ZERO.
021500 77  CSN-ZERO             PIC 9 VALUE ZERO.
021600*---------------------------------------------------------------*
021700 EJECT
021800*===============================================================*
021900 PROCEDURE DIVISION.
022000*===============================================================*
022100 MAIN-PROGRAM.
022200*OPEN-FILES.
022300     PERFORM 010-OPEN THRU
022400             010-OPEN-EXIT.
022500 PROCESS-PARMS.
022600     DISPLAY 'READING PARM FILE'.
022700     PERFORM 050-RD-PARM-FILE THRU
022800             050-RD-PARM-FILE-EXIT
022900             UNTIL CC-PARM = CC-EOF.
023000 INIT-RD-XREF.
023100     PERFORM 110-RD-XREF-FILE THRU
023200             110-RD-XREF-FILE-EXIT.
023300 GET-REPORTS.
023400     PERFORM 150-LOAD-REPORTS THRU
023500             150-LOAD-REPORTS-EXIT.
023600 MAIN-PROCESS.
023700     DISPLAY 'MAIN PROCESS'.
023800     PERFORM 100-MAIN-PROCESS THRU
023900             100-MAIN-PROCESS-EXIT
024000             UNTIL CC-XREF = CC-EOF.
024100 UNMATCHED-PROCESS.
024200     PERFORM 200-UNMATCHED-REPORTS THRU
024300             200-UNMATCHED-REPORTS-EXIT.
024400 CLOSE-FILES.
024500     PERFORM 900-CLOSE THRU
024600             900-CLOSE-EXIT.
024700 FINISH-PROGRAM.
024800     STOP RUN.
024900 EJECT
025000*---------------------------------------------------------------*
025100* OPEN FILES
025200*---------------------------------------------------------------*
025300 010-OPEN.
025400     DISPLAY 'OPEN FILES'.
025500 010-PARM-FILE.
025600     OPEN INPUT  PARM-FILE.
025700     MOVE CC-PARM TO CC-STATUS.
025800     IF CC-STATUS NOT = CC-OK
025900         DISPLAY 'ERROR,OPEN,PARM-FILE,CC: ' CC-STATUS
026000         PERFORM 999-CANCEL THRU
026100                 999-CANCEL-EXIT
026200     END-IF.
026300 010-XREF-FILE.
026400     OPEN INPUT  XREF-FILE.
026500     MOVE CC-XREF TO CC-STATUS.
026600     IF CC-STATUS NOT = CC-OK
026700         DISPLAY 'ERROR,OPEN,XREF-FILE,CC: ' CC-STATUS
026800         PERFORM 999-CANCEL THRU
026900                 999-CANCEL-EXIT
027000     END-IF.
027100 010-PSYM-FILE.
027200     OPEN INPUT  PSYM-FILE.
027300     MOVE CC-PSYM TO CC-STATUS.
027400     IF CC-STATUS NOT = CC-OK
027500         DISPLAY 'ERROR,OPEN,PSYM-FILE,CC: ' CC-STATUS
027600         PERFORM 999-CANCEL THRU
027700                 999-CANCEL-EXIT
027800     END-IF.
027900 010-RPT-FILE.
028000     OPEN INPUT RPT-FILE.
028100     MOVE CC-RPT TO CC-STATUS.
028200     IF CC-STATUS NOT = CC-OK
028300         DISPLAY 'ERROR,OPEN,RPT-FILE,CC: ' CC-STATUS
028400         PERFORM 999-CANCEL THRU
028500                 999-CANCEL-EXIT
028600     END-IF.
028700 010-DOC-FILE.
028800     OPEN OUTPUT DOC-FILE.
028900     MOVE CC-DOC TO CC-STATUS.
029000     IF CC-STATUS NOT = CC-OK
029100         DISPLAY 'ERROR,OPEN,DOC-FILE,CC: ' CC-STATUS
029200         PERFORM 999-CANCEL THRU
029300                 999-CANCEL-EXIT
029400     END-IF.
029500 010-OPEN-EXIT. EXIT.
029600 EJECT
029700*---------------------------------------------------------------*
029800* READ-PARMS.
029900*---------------------------------------------------------------*
030000 050-RD-PARM-FILE.
030100     READ PARM-FILE.
030200     MOVE CC-PARM TO CC-STATUS.
030300     IF CC-STATUS = CC-OK
030400         ADD 1 TO AC-PARM
030500         DISPLAY PARM-REC
030600     ELSE
030700         IF CC-STATUS = CC-EOF
030800             GO TO 050-RD-PARM-FILE-EXIT
030900         ELSE
031000             DISPLAY 'ERROR,READ,XREF-FILE,CC: ' CC-STATUS
031100             PERFORM 999-CANCEL THRU
031200                     999-CANCEL-EXIT
031300         END-IF
031400     END-IF.
031500 050-SAVE-PARMS.
031600     IF P-JOB
031700         MOVE P-DATA (1:8)  TO FH-P-JOB
031800     END-IF.
031900     IF P-JOB-STEP
032000         MOVE P-DATA (1:8)  TO FH-P-JOB-STEP
032100     END-IF.
032200     IF P-JOB-DESC
032300         MOVE P-DATA (1:50)  TO FH-P-JOB-DESC
032400     END-IF.
032500     IF P-PROC
032600         MOVE P-DATA (1:8)  TO FH-P-PROC
032700     END-IF.
032800     IF P-SYS
032900         MOVE P-DATA (1:20) TO FH-P-SYS
033000     END-IF.
033100     IF P-SYS-SUB
033200         MOVE P-DATA (1:20) TO FH-P-SYS-SUB
033300     END-IF.
033400     IF P-USER
033500         MOVE P-DATA (1:9)  TO FH-P-USER
033600     END-IF.
033700     IF P-EXT
033800         MOVE P-DATA (1:3)  TO FH-P-EXT
033900     END-IF.
034000     IF P-DEPT
034100         MOVE P-DATA (1:4)  TO FH-P-DEPT
034200     END-IF.
034300     IF P-DSN-DESC
034400         MOVE P-DATA (1:50)  TO FH-P-DSN-DESC
034500     END-IF.
034600     IF P-RPT-DESC
034700         MOVE P-DATA (1:22)  TO FH-P-RPT-DESC
034800     END-IF.
034900 050-RD-PARM-FILE-EXIT. EXIT.
035000 EJECT
035100*---------------------------------------------------------------*
035200* MAIN PROCESS
035300*---------------------------------------------------------------*
035400 100-MAIN-PROCESS.
035500*100-SELECT-PROC.
035600     IF X-PROC NOT = FH-P-PROC
035700          GO TO 100-MAIN-PROCESS-EXIT
035800     END-IF.
035900 100-BYPASS.
036000     IF X-DDNAME =  SPACES    OR
036100        X-DDNAME = 'SYSOUC  ' OR
036200        X-DDNAME = 'SYSOUX  ' OR
036300        X-DDNAME = 'SNAPER  ' OR
036400        X-DDNAME = 'SYSABOUT' OR
036500        X-DDNAME = 'SYSDBOUT' OR
036600        X-DDNAME = 'SYSUDUMP' OR
036700        X-DDNAME = '$ORTPARM' OR
036800        X-DDNAME = 'SORTLIB ' OR
036900        X-DDNAME = 'STEPLIB ' OR
037000        X-DDNAME = 'DRLIB   '
037100          GO TO 100-READ
037200     END-IF.
037300 100-CONSTANT-LOGIC.
037400     MOVE SPACES        TO DOC-REC.
037500     MOVE FH-P-JOB      TO D-JOB.
037600     MOVE FH-P-JOB-STEP TO D-JOB-STEP.
037700     MOVE X-PROC        TO D-PROC.
037800     MOVE X-PROC-STEP   TO D-PROC-STEP.
037900     MOVE X-PGM         TO D-PGM.
038000     MOVE X-DDNAME      TO D-DDNAME.
038100     MOVE FH-P-SYS      TO D-SYS.
038200     MOVE FH-P-SYS-SUB  TO D-SYS-SUB.
038300 100-RESOLVE-SYMBOLICS.
038400     MOVE X-DSN-OR-SYSOUT TO WS-UNRESOLVED
038500     PERFORM 120-RESOLVE-SYMBOLICS THRU
038600             120-RESOLVE-SYMBOLICS-EXIT.
038700     MOVE WS-RESOLVED TO WS-DSN-OR-SYSOUT.
038800 100-IEFBR14.
038900     IF X-PGM = 'IEFBR14 '
039000         IF WS-DSN-OR-SYSOUT (1:7) = 'SYSOUT='
039100             GO TO 100-READ
039200         END-IF
039300         MOVE 'O'                  TO D-SORT-02
039400         MOVE WS-DSN-OR-SYSOUT     TO D-DSN
039500         GO TO 100-WRITE
039600     END-IF.
039700 100-IDCAMS.
039800     IF X-PGM = 'IDCAMS  '
039900         IF X-DDNAME = 'SYSPRINT'
040000             MOVE 'R'                  TO D-SORT-01
040100             MOVE 'IDCAMS DIAGNOSTICS' TO D-RPT-TITLE
040200             MOVE X-PGM                TO D-RPT-NO
040300             MOVE 'C/R'                TO D-RPT-DEPT
040400             MOVE 'O'                  TO D-RPT-CLASS
040500             MOVE '1'                  TO D-RPT-COPIES
040600             MOVE 'OPER'               TO D-RPT-USER
040700             MOVE '166'                TO D-RPT-EXT
040800             MOVE 'L'                  TO D-RPT-LOCATION
040900             GO TO 100-WRITE
041000         END-IF
041100         IF X-DDNAME = 'SYSIN   '
041200             MOVE 'I'                  TO D-SORT-02
041300             MOVE 'IDCAMS PARAMETERS'  TO D-COMMENT
041400             MOVE WS-DSN-OR-SYSOUT     TO D-DSN
041500             GO TO 100-WRITE
041600         END-IF
041700     END-IF.
041800 100-ICEMAN.
041900     IF X-PGM = 'ICEMAN  '
042000         IF X-DDNAME = 'SYSOUT  '
042100             MOVE 'R'                  TO D-SORT-01
042200             MOVE 'SORT DIAGNOSTICS'   TO D-RPT-TITLE
042300             MOVE X-PGM                TO D-RPT-NO
042400             MOVE 'C/R'                TO D-RPT-DEPT
042500             MOVE 'O'                  TO D-RPT-CLASS
042600             MOVE '1'                  TO D-RPT-COPIES
042700             MOVE 'OPER'               TO D-RPT-USER
042800             MOVE '166'                TO D-RPT-EXT
042900             MOVE 'L'                  TO D-RPT-LOCATION
043000             GO TO 100-WRITE
043100         END-IF
043200         IF X-DDNAME = 'SYSIN   '
043300             MOVE 'I'                  TO D-SORT-02
043400             MOVE 'SORT PARAMETERS'    TO D-COMMENT
043500             MOVE WS-DSN-OR-SYSOUT     TO D-DSN
043600             GO TO 100-WRITE
043700         END-IF
043800         IF X-DDNAME = 'SORTIN  '
043900             MOVE 'I'                  TO D-SORT-02
044000             MOVE 'SORT INPUT FILE'    TO D-COMMENT
044100             MOVE WS-DSN-OR-SYSOUT     TO D-DSN
044200             GO TO 100-WRITE
044300         END-IF
044400         IF X-DDNAME = 'SORTOUT '
044500             MOVE 'O'                  TO D-SORT-02
044600             MOVE 'SORT OUTPUT FILE'   TO D-COMMENT
044700             MOVE WS-DSN-OR-SYSOUT     TO D-DSN
044800             GO TO 100-WRITE
044900         END-IF
045000     END-IF.
045100 100-XT010BSU.
045200     IF X-PGM = 'XT010BSU'
045300         IF X-DDNAME = 'SYSUT1  '
045400             MOVE 'I'                  TO D-SORT-02
045500             MOVE 'INPUT DATASET'      TO D-COMMENT
045600             MOVE WS-DSN-OR-SYSOUT     TO D-DSN
045700             GO TO 100-WRITE
045800         END-IF
045900         IF X-DDNAME = 'SYSUT2  '
046000             MOVE 'O'                  TO D-SORT-02
046100             MOVE 'OUTPUT DATASET'     TO D-COMMENT
046200             MOVE WS-DSN-OR-SYSOUT     TO D-DSN
046300             GO TO 100-WRITE
046400         END-IF
046500     END-IF.
046600 100-REPORTS.
046700     IF WS-DSN-OR-SYSOUT (1:7) NOT = 'SYSOUT='
046800         GO TO 100-DATASETS
046900     END-IF.
047000***
047100***  REPORT INIT *
047200     MOVE 'R'                  TO D-SORT-01
047300     MOVE 'N'                  TO SW-FOUND
047400***
047500***  PROCESS THE CATALOGED REPORTS *
047600     PERFORM VARYING WS-IDX FROM 1 BY 1
047700             UNTIL WS-IDX > REPORT-TBL-SIZE
047800        IF RT-NO (WS-IDX) = X-DDNAME
047900            MOVE 'Y'                  TO SW-FOUND
048000            MOVE 'Y'                  TO RT-USED (WS-IDX)
048100            MOVE RT-TITLE (WS-IDX)    TO D-RPT-TITLE
048200            MOVE RT-NO (WS-IDX)       TO D-RPT-NO
048300            MOVE RT-DEPT (WS-IDX)     TO D-RPT-DEPT
048400            MOVE RT-CLASS (WS-IDX)    TO D-RPT-CLASS
048500            MOVE RT-COPIES (WS-IDX)   TO D-RPT-COPIES
048600            MOVE RT-USER (WS-IDX)     TO D-RPT-USER
048700            MOVE RT-EXT (WS-IDX)      TO D-RPT-EXT
048800            MOVE RT-COMMENT (WS-IDX)  TO D-COMMENT
048900            MOVE RT-LOCATION (WS-IDX) TO D-RPT-LOCATION
049000        END-IF
049100     END-PERFORM.
049200***
049300***  PUT THE ACTUAL CLASS IN THE REPORT FIELD *
049400     IF WS-DSN-OR-SYSOUT (8:1) = '('
049500         MOVE WS-DSN-OR-SYSOUT (8:2) TO D-RPT-CLASS
049600         IF D-COMMENT = SPACES
049700             MOVE ZERO TO WS-POS
049800             PERFORM VARYING WS-IDX FROM 9 BY 1
049900                     UNTIL WS-IDX = 50 OR
050000                     WS-DSN-OR-SYSOUT (WS-IDX:1) = ')'
050100                 ADD 1 TO WS-POS
050200                 MOVE WS-DSN-OR-SYSOUT (WS-IDX:1) TO
050300                      D-COMMENT (WS-POS:1)
050400             END-PERFORM
050500         END-IF
050600     ELSE
050700         MOVE WS-DSN-OR-SYSOUT (8:1) TO D-RPT-CLASS
050800     END-IF.
050900     IF D-RPT-CLASS = '*'
051000         MOVE 'O' TO D-RPT-CLASS
051100     END-IF.
051200***
051300***  USER OR A CATALOGED REPORT - APPLY DEFAULTS TO BLANKS *
051400     IF WS-DSN-OR-SYSOUT (1:8) NOT = 'SYSOUT=*' OR
051500        SW-FOUND = 'Y'
051600         IF D-RPT-TITLE = SPACES
051700               MOVE FH-P-RPT-DESC        TO D-RPT-TITLE
051800         END-IF
051900         IF D-RPT-NO = SPACES
052000             MOVE X-PGM                  TO D-RPT-NO
052100         END-IF
052200         IF D-RPT-DEPT = SPACES
052300               MOVE FH-P-DEPT            TO D-RPT-DEPT
052400         END-IF
052500         IF D-RPT-COPIES = SPACES
052600               MOVE '1'                  TO D-RPT-COPIES
052700         END-IF
052800         IF D-RPT-USER = SPACES
052900               MOVE FH-P-USER            TO D-RPT-USER
053000         END-IF
053100         IF D-RPT-EXT = SPACES
053200               MOVE FH-P-EXT             TO D-RPT-EXT
053300         END-IF
053400         IF D-COMMENT = SPACES
053500               MOVE 'NONE'               TO D-COMMENT
053600         END-IF
053700         IF D-RPT-LOCATION = SPACES
053800               MOVE 'L'                  TO D-RPT-LOCATION
053900         END-IF
054000         GO TO 100-WRITE
054100     END-IF.
054200***
054300***  NON USER REPORT - CLASS EQUALS MSGCLASS - SET DEFAULTS
054400     IF X-COMMENT NOT = SPACES
054500         IF X-COMMENT (1:2) = 'O '
054600             IF X-COMMENT (3:47) NOT = SPACE
054700                 MOVE X-COMMENT (3:22) TO D-RPT-TITLE
054800              ELSE
054900                  MOVE 'DIAGNOSTICS'    TO D-RPT-TITLE
055000              END-IF
055100         ELSE
055200             MOVE X-COMMENT (1:22)     TO D-RPT-TITLE
055300     END-IF.
055400     MOVE X-PGM                TO D-RPT-NO.
055500     MOVE 'C/R'                TO D-RPT-DEPT.
055600     MOVE 'O'                  TO D-RPT-CLASS.
055700     MOVE '1'                  TO D-RPT-COPIES.
055800     MOVE 'OPER'               TO D-RPT-USER.
055900     MOVE '166'                TO D-RPT-EXT.
056000     MOVE 'L'                  TO D-RPT-LOCATION.
056100     MOVE 'NONE'               TO D-COMMENT.
056200     GO TO 100-WRITE.
056300***
056400 100-DATASETS.
056500     MOVE WS-DSN-OR-SYSOUT TO D-DSN.
056600***
056700***  DETERMINE IF INPUT/OUTPUT DATASET *
056800     MOVE 'N' TO SW-FINI.
056900     PERFORM VARYING WS-IDX FROM 8 BY -1 UNTIL SW-FINI = 'Y'
057000        IF WS-IDX = 1 OR
057100           X-DDNAME (WS-IDX:1) NOT = SPACE
057200            MOVE 'Y' TO SW-FINI
057300        END-IF
057400     END-PERFORM.
057500     IF X-DDNAME (WS-IDX:1) = 'I'
057600         MOVE 'I' TO D-SORT-02
057700     END-IF.
057800     IF X-DDNAME (WS-IDX:1) = 'O' OR
057900        X-DDNAME (WS-IDX:1) = 'U'
058000         MOVE 'O' TO D-SORT-02
058100     END-IF.
058200     IF D-SORT-02 = SPACES
058300         IF X-COMMENT (1:2) = 'I '
058400             MOVE 'I' TO D-SORT-02
058500         END-IF
058600         IF X-COMMENT (1:2) = 'O ' OR
058700            X-COMMENT (1:2) = 'U '
058800             MOVE 'O' TO D-SORT-02
058900         END-IF
059000     END-IF.
059100***
059200***  IF INPUT/OUTPUT DETERMINATION COULD NOT BE MADE - SET
059300***  TO OUTPUT *
059400     IF D-SORT-02 = SPACES
059500         MOVE 'I' TO D-SORT-02
059600     END-IF.
059700***
059800***  PROCESS DATA SET COMMENT
059900     IF X-COMMENT (3:50) NOT = SPACE
060000         MOVE X-COMMENT     TO D-COMMENT
060100     ELSE
060200         MOVE FH-P-DSN-DESC TO D-COMMENT
060300     END-IF.
060400 100-WRITE.
060500     PERFORM 115-WR-DOC-FILE THRU
060600             115-WR-DOC-FILE-EXIT.
060700 100-READ.
060800     PERFORM 110-RD-XREF-FILE THRU
060900             110-RD-XREF-FILE-EXIT.
061000 100-MAIN-PROCESS-EXIT. EXIT.
061100 EJECT
061200*---------------------------------------------------------------*
061300* READ XREF FILE
061400*---------------------------------------------------------------*
061500 110-RD-XREF-FILE.
061600     READ XREF-FILE.
061700     MOVE CC-XREF TO CC-STATUS.
061800     IF CC-STATUS = CC-OK
061900         ADD 1 TO AC-XREF
062000     ELSE
062100         IF CC-STATUS = CC-EOF
062200             GO TO 110-RD-XREF-FILE-EXIT
062300         ELSE
062400             DISPLAY 'ERROR,READ,XREF-FILE,CC: ' CC-STATUS
062500             PERFORM 999-CANCEL THRU
062600                     999-CANCEL-EXIT.
062700 110-RD-XREF-FILE-EXIT. EXIT.
062800 EJECT
062900*---------------------------------------------------------------*
063000* WRITE DOC-FILE
063100*---------------------------------------------------------------*
063200 115-WR-DOC-FILE.
063300     WRITE DOC-REC.
063400     MOVE CC-DOC TO CC-STATUS.
063500     IF CC-STATUS = CC-OK
063600         ADD 1 TO AC-DOC
063700     ELSE
063800         DISPLAY 'ERROR,WRITE,DOC-FILE,CC: ' CC-STATUS
063900         PERFORM 999-CANCEL THRU
064000                 999-CANCEL-EXIT.
064100 115-WR-DOC-FILE-EXIT. EXIT.
064200 EJECT
064300*---------------------------------------------------------------*
064400* RESOLVE DATA SETS
064500*---------------------------------------------------------------*
064600 120-RESOLVE-SYMBOLICS.
064700     MOVE SPACES TO WS-RESOLVED.
064800 120-TEMP-DSN.
064900     IF WS-UNRESOLVED (1:2) = '&&'
065000         MOVE WS-UNRESOLVED TO WS-RESOLVED
065100         GO TO 120-RESOLVE-SYMBOLICS-EXIT
065200     END-IF.
065300 120-LOOP-INIT.
065400     MOVE ZERO TO WS-POS.
065500     MOVE ZERO TO WS-POS-RS.
065600***
065700***
065800***
065900 120-LOOP.
066000     ADD 1 TO WS-POS.
066100     IF WS-POS > 50
066200         GO TO 120-RESOLVE-SYMBOLICS-EXIT
066300     END-IF.
066400***
066500     IF WS-UNRESOLVED (WS-POS:1) = SPACE
066600         GO TO 120-RESOLVE-SYMBOLICS-EXIT
066700     END-IF.
066800***
066900     IF WS-UNRESOLVED (WS-POS:1) NOT = '&'
067000         ADD 1 TO WS-POS-RS
067100         IF WS-POS-RS > 50
067200             DISPLAY 'ERROR,XREF-REC,OVFL1: ' XREF-REC
067300             GO TO 120-RESOLVE-SYMBOLICS-EXIT
067400         END-IF
067500         MOVE WS-UNRESOLVED (WS-POS:1)
067600           TO WS-RESOLVED (WS-POS-RS:1)
067700         GO TO 120-LOOP
067800     END-IF.
067900***
068000***  ISOLATE AND COMPUTE THE LENGTH OF THE SYMBOLIC *
068100     COMPUTE WS-POS = WS-POS + 1.
068200     COMPUTE WS-LEN = 50 - WS-POS + 1.
068300     IF WS-LEN > 7
068400         MOVE 7 TO WS-LEN
068500     END-IF.
068600     MOVE WS-UNRESOLVED (WS-POS:WS-LEN) TO WS-SYMBOLIC.
068700     MOVE ZERO TO WS-POS-OFFSET.
068800     MOVE 'N'  TO SW-FINI.
068900     PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL SW-FINI = 'Y'
069000       IF WS-IDX = 7
069100           MOVE 'Y' TO SW-FINI
069200       END-IF
069300       MOVE WS-IDX TO WS-LEN-SYM
069400       IF WS-SYMBOLIC (WS-IDX:1) = '(' OR
069500          WS-SYMBOLIC (WS-IDX:1) = ')' OR
069600          WS-SYMBOLIC (WS-IDX:1) = '&' OR
069700          WS-SYMBOLIC (WS-IDX:1) = '.' OR
069800          WS-SYMBOLIC (WS-IDX:1) = SPACE
069900           COMPUTE WS-LEN-SYM = WS-IDX - 1
070000           COMPUTE AC-SPACES = 7 - WS-LEN-SYM
070100           MOVE SPACES TO WS-SYMBOLIC (WS-IDX:AC-SPACES)
070200           MOVE 'Y' TO SW-FINI
070300           IF WS-SYMBOLIC (WS-IDX:1) = '.'
070400               MOVE 1 TO WS-POS-OFFSET
070500           END-IF
070600       END-IF
070700     END-PERFORM.
070800***
070900***  RESOLVE THE SYMBOLIC *
071000     MOVE X-PROC      TO PSYM-PROC.
071100     MOVE WS-SYMBOLIC TO PSYM-SYMBOLIC.
071200     PERFORM 130-RD-PSYM-FILE THRU
071300             130-RD-PSYM-FILE-EXIT.
071400***
071500***  MOVE RESOLVED SYMBOLIC VALUE TO WS-RESOLVED *
071600     MOVE 'N' TO SW-FINI.
071700     PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL SW-FINI = 'Y'
071800           IF WS-IDX = 40
071900               MOVE 'Y' TO SW-FINI
072000           END-IF
072100           IF PSYM-VALUE (WS-IDX:1) = SPACE
072200               MOVE 'Y' TO SW-FINI
072300           ELSE
072400               ADD 1 TO WS-POS-RS
072500               IF WS-POS-RS > 50
072600                   MOVE 'Y' TO SW-FINI
072700                   DISPLAY 'ERROR,XREF-REC,OVFL2: ' XREF-REC
072800                   MOVE 99 TO WS-IDX
072900               END-IF
073000               MOVE PSYM-VALUE (WS-IDX:1)
073100                 TO WS-RESOLVED (WS-POS-RS:1)
073200           END-IF
073300     END-PERFORM.
073400***
073500***  CHECK FOR BAD LOOP RETURN CODE *
073600     IF WS-IDX = 99
073700         GO TO 120-RESOLVE-SYMBOLICS-EXIT
073800     END-IF.
073900***
074000***  COMPUTE POSITION PRECEEDING THE NEXT SCAN POSITION
074100***  CALCULATED AFTER THE BRANCH TO 120-LOOP *
074200     COMPUTE WS-POS = WS-POS +
074300                      WS-POS-OFFSET + WS-LEN-SYM - 1.
074400     GO TO 120-LOOP.
074500 120-RESOLVE-SYMBOLICS-EXIT. EXIT.
074600 EJECT
074700*---------------------------------------------------------------*
074800* READ SYMBOLICS FILE
074900*---------------------------------------------------------------*
075000 130-RD-PSYM-FILE.
075100     READ PSYM-FILE.
075200     MOVE CC-PSYM TO CC-STATUS.
075300     IF CC-STATUS = CC-OK
075400         ADD 1 TO AC-XREF
075500     ELSE
075600         IF CC-STATUS = CC-NOT-FOUND
075700             GO TO 130-RD-PSYM-FILE-EXIT
075800         ELSE
075900             DISPLAY 'ERROR,READ,PSYM-FILE,CC: ' CC-STATUS
076000             PERFORM 999-CANCEL THRU
076100                     999-CANCEL-EXIT.
076200 130-RD-PSYM-FILE-EXIT. EXIT.
076300 EJECT
076400*---------------------------------------------------------------*
076500* GET REPORTS FROM DATABASE FOR THIS JOB.
076600*---------------------------------------------------------------*
076700 150-LOAD-REPORTS.
076800     DISPLAY 'READING REPORTS FOR ' FH-P-JOB.
076900 150-START.
077000     MOVE FH-P-JOB TO R-KEY.
077100     START RPT-FILE KEY IS NOT LESS THAN R-KEY.
077200     IF CC-STATUS NOT = CC-OK
077300         DISPLAY 'WARN,START POSITION: ' FH-P-JOB
077400         GO TO 150-LOAD-REPORTS-EXIT
077500     END-IF.
077600 150-INIT-TABLE.
077700     MOVE ZERO TO REPORT-TBL-SIZE.
077800     MOVE ZERO TO REPORT-IDX.
077900 150-READ-LOOP.
078000     READ RPT-FILE NEXT.
078100     MOVE CC-RPT TO CC-STATUS.
078200     IF CC-STATUS = CC-OK
078300         ADD 1 TO AC-RPT
078400     ELSE
078500         IF CC-STATUS = CC-EOF
078600             GO TO 150-LOAD-REPORTS-EXIT
078700         ELSE
078800             DISPLAY 'ERROR,READ,RPT-FILE,CC: ' CC-STATUS
078900             PERFORM 999-CANCEL THRU
079000                     999-CANCEL-EXIT
079100         END-IF
079200     END-IF.
079300 150-ADD-REPORT.
079400     IF R-JOB = FH-P-JOB
079500         ADD 1 TO REPORT-IDX
079600         ADD 1 TO REPORT-TBL-SIZE
079700         IF REPORT-IDX > REPORT-IDX-MAX
079800             DISPLAY 'ERROR,REPORT-TABLE,OVFLO'
079900             PERFORM 999-CANCEL THRU
080000                     999-CANCEL-EXIT
080100         END-IF
080200         MOVE RPT-REC TO RT-ENTRY (REPORT-IDX)
080300         MOVE SPACE   TO RT-USED  (REPORT-IDX)
080400         DISPLAY RPT-REC
080500     ELSE
080600         GO TO 150-LOAD-REPORTS-EXIT
080700     END-IF.
080800 150-LOOP-BACK.
080900     GO TO 150-READ-LOOP.
081000 150-LOAD-REPORTS-EXIT. EXIT.
081100 EJECT
081200*---------------------------------------------------------------*
081300* PROCESS UNMATCHED CATALOGED REPORTS
081400*---------------------------------------------------------------*
081500 200-UNMATCHED-REPORTS.
081600     DISPLAY 'UNMATCHED PROCESS'.
081700     PERFORM VARYING WS-IDX FROM 1 BY 1
081800             UNTIL WS-IDX > REPORT-TBL-SIZE
081900        IF RT-USED (WS-IDX) = SPACE
082000            MOVE SPACES               TO DOC-REC
082100            MOVE 'R'                  TO D-SORT-01
082200            MOVE FH-P-JOB             TO D-JOB
082300            MOVE FH-P-JOB             TO D-DDNAME
082400            MOVE FH-P-JOB-STEP        TO D-JOB-STEP
082500            MOVE FH-P-PROC            TO D-PROC
082600            MOVE FH-P-SYS             TO D-SYS
082700            MOVE FH-P-SYS-SUB         TO D-SYS-SUB
082800            MOVE 'PSXXXXXX'           TO D-PROC-STEP
082900            MOVE RT-TITLE (WS-IDX)    TO D-RPT-TITLE
083000            MOVE RT-NO (WS-IDX)       TO D-RPT-NO
083100            MOVE RT-DEPT (WS-IDX)     TO D-RPT-DEPT
083200            MOVE RT-CLASS (WS-IDX)    TO D-RPT-CLASS
083300            MOVE RT-COPIES (WS-IDX)   TO D-RPT-COPIES
083400            MOVE RT-USER (WS-IDX)     TO D-RPT-USER
083500            MOVE RT-EXT (WS-IDX)      TO D-RPT-EXT
083600            MOVE RT-COMMENT (WS-IDX)  TO D-COMMENT
083700            MOVE RT-LOCATION (WS-IDX) TO D-RPT-LOCATION
083800            IF D-RPT-TITLE = SPACES
083900                MOVE FH-P-RPT-DESC    TO D-RPT-TITLE
084000            END-IF
084100            IF D-RPT-NO = SPACES
084200                MOVE FH-P-JOB         TO D-RPT-NO
084300            END-IF
084400            IF D-RPT-DEPT = SPACES
084500                MOVE FH-P-DEPT        TO D-RPT-DEPT
084600            END-IF
084700            IF D-RPT-COPIES = SPACES
084800                MOVE '1'              TO D-RPT-COPIES
084900            END-IF
085000            IF D-RPT-USER = SPACES
085100                MOVE FH-P-USER        TO D-RPT-USER
085200            END-IF
085300            IF D-RPT-EXT = SPACES
085400                MOVE FH-P-EXT         TO D-RPT-EXT
085500            END-IF
085600            IF D-COMMENT = SPACES
085700                MOVE 'NONE'           TO D-COMMENT
085800            END-IF
085900            IF D-RPT-LOCATION = SPACES
086000                MOVE 'L'              TO D-RPT-LOCATION
086100            END-IF
086200            PERFORM 115-WR-DOC-FILE THRU
086300                    115-WR-DOC-FILE-EXIT
086400        END-IF
086500     END-PERFORM.
086600 200-UNMATCHED-REPORTS-EXIT. EXIT.
086700 EJECT
086800*---------------------------------------------------------------*
086900* CLOSE FILES
087000*---------------------------------------------------------------*
087100 900-CLOSE.
087200     DISPLAY 'CLOSE FILES'.
087300 900-PARM-FILE.
087400     CLOSE PARM-FILE.
087500     MOVE CC-PARM TO CC-STATUS.
087600     IF CC-STATUS NOT = CC-OK
087700         DISPLAY 'ERROR,CLOSE,PARM-FILE,CC: ' CC-STATUS
087800         PERFORM 999-CANCEL THRU
087900                 999-CANCEL-EXIT
088000     END-IF.
088100 900-XREF-FILE.
088200     CLOSE XREF-FILE.
088300     MOVE CC-XREF TO CC-STATUS.
088400     IF CC-STATUS NOT = CC-OK
088500         DISPLAY 'ERROR,CLOSE,XREF-FILE,CC: ' CC-STATUS
088600         PERFORM 999-CANCEL THRU
088700                 999-CANCEL-EXIT
088800     END-IF.
088900 900-PSYM-FILE.
089000     CLOSE PSYM-FILE.
089100     MOVE CC-PSYM TO CC-STATUS.
089200     IF CC-STATUS NOT = CC-OK
089300         DISPLAY 'ERROR,CLOSE,PSYM-FILE,CC: ' CC-STATUS
089400         PERFORM 999-CANCEL THRU
089500                 999-CANCEL-EXIT
089600     END-IF.
089700 900-RPT-FILE.
089800     CLOSE RPT-FILE.
089900     MOVE CC-RPT TO CC-STATUS.
090000     IF CC-STATUS NOT = CC-OK
090100         DISPLAY 'ERROR,CLOSE,RPT-FILE,CC: ' CC-STATUS
090200         PERFORM 999-CANCEL THRU
090300                 999-CANCEL-EXIT
090400     END-IF.
090500 900-DOC-FILE.
090600     CLOSE DOC-FILE.
090700     MOVE CC-DOC TO CC-STATUS.
090800     IF CC-STATUS NOT = CC-OK
090900         DISPLAY 'ERROR,CLOSE,DOC-FILE,CC: ' CC-STATUS
091000         PERFORM 999-CANCEL THRU
091100                 999-CANCEL-EXIT
091200     END-IF.
091300 900-CLOSE-EXIT. EXIT.
091400 EJECT
091500*---------------------------------------------------------------*
091600* CANCEL ROUTINE
091700*---------------------------------------------------------------*
091800 999-CANCEL.
091900     DISPLAY '*******  XRB010,ABNORMAL END  *******'.
092000     COMPUTE CSN-ZERO = CSN-ZERO / CSN-ZERO.
092100 999-CANCEL-EXIT. EXIT.

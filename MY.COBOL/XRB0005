000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. XRB0005.
000300 AUTHOR. TONY REY.
000400 INSTALLATION. TONY REY INC.
000500 DATE-WRITTEN. 11/05/91.
000600 DATE-COMPILED.
000700*================================================================*
000800* PROGRAM NARRATIVE
000900*================================================================*
001000* PRODUCE JCL LIBRARY CROSS REFERENCE FILE.
001100*================================================================*
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SOURCE-COMPUTER.  IBM-370.
001500 OBJECT-COMPUTER.  IBM-370.
001600 SPECIAL-NAMES. C01 IS TOP-OF-PAGE.
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900     SELECT IN-FILE            ASSIGN TO UT-S-INFILE
002000                               FILE STATUS IS IN-CC.
002100     SELECT XREF-FILE          ASSIGN TO UT-S-XREF
002200                               FILE STATUS IS XREF-CC.
002300     SELECT PRINT-FILE         ASSIGN TO UT-S-PRINT.
002400 DATA DIVISION.
002500 FILE SECTION.
002600*---------------------------------------------------------------*
002700 FD  IN-FILE
002800     BLOCK CONTAINS 0 RECORDS
002900     LABEL RECORDS ARE STANDARD.
003000 01  IN-REC              PIC X(81).
003100 01  IN-REC-R.
003200     05  FILLER          PIC X(01).
003300     05  IN-SL-SL        PIC X(02).
003400     05  IN-LINE.
003500         10 IN-ASTER     PIC X(01).
003600         10 FILLER       PIC X(69).
003700     05  IN-LINE-POS     REDEFINES IN-LINE
003800                         OCCURS 70 TIMES
003900                         PIC X(01).
004000     05  IN-SEQ          PIC X(08).
004100*---------------------------------------------------------------*
004200 FD  XREF-FILE
004300     BLOCK CONTAINS 0 RECORDS
004400     LABEL RECORDS ARE STANDARD.
004500 01  X-REC.
004600     05 X-JOB              PIC X(08).
004700     05 FILLER             PIC X(01).
004800     05 X-JOB-STEP         PIC X(08).
004900     05 FILLER             PIC X(01).
005000     05 X-PROC-PGM-FLAG    PIC X(04).
005100     05 FILLER             PIC X(01).
005200     05 X-PROC-PGM         PIC X(08).
005300     05 FILLER             PIC X(01).
005400     05 X-DDNAME           PIC X(17).
005500     05 FILLER             PIC X(01).
005600     05 X-DSN-OR-SYSOUT    PIC X(50).
005700     05 FILLER             PIC X(01).
005800     05 X-COMMENT          PIC X(70).
005900*---------------------------------------------------------------*
006000 FD  PRINT-FILE
006100     BLOCK CONTAINS 0 RECORDS
006200     LABEL RECORDS ARE STANDARD.
006300 01  PRINT-REC          PIC X(133).
006400 EJECT
006500*===============================================================*
006600 WORKING-STORAGE SECTION.
006700*===============================================================*
006800 77  IN-CC                 PIC XX VALUE SPACES.
006900 77  XREF-CC               PIC XX VALUE SPACES.
007000 COPY CCFILE.
007100*---------------------------------------------------------------*
007200 77  SW-PROCESS-SYMBOLIC   PIC X            VALUE 'N'.
007300 77  SW-EXEC-NAKED-Y-N     PIC X(01)        VALUE 'N'.
007400*---------------------------------------------------------------*
007500 77  AC-LEFT-PAREN         PIC 99           VALUE ZERO.
007600 77  AC-RIGHT-PAREN        PIC 99           VALUE ZERO.
007700 77  AC-LEFT-QUOTE         PIC 99           VALUE ZERO.
007800 77  AC-RIGHT-QUOTE        PIC 99           VALUE ZERO.
007900 01  AC-RECORDS.
008000     05  FILLER            PIC X(20)        VALUE
008100         'IN RECORDS: '.
008200     05  AC-IN             PIC 9(7)         VALUE ZERO.
008300     05  FILLER            PIC X(02)        VALUE SPACES.
008400     05  FILLER            PIC X(20)        VALUE
008500         'XREF RECORDS: '.
008600     05  AC-XREF           PIC 9(7)         VALUE ZERO.
008700*---------------------------------------------------------------*
008800 01  POS-TALLY.
008900     05  POS-SPACES        PIC 9(03)        VALUE ZERO.
009000     05  POS-JOB           PIC 9(03)        VALUE ZERO.
009100     05  POS-EXEC          PIC 9(03)        VALUE ZERO.
009200     05  POS-PGM           PIC 9(03)        VALUE ZERO.
009300     05  POS-DD            PIC 9(03)        VALUE ZERO.
009400     05  POS-DSN           PIC 9(03)        VALUE ZERO.
009500     05  POS-SYSOUT        PIC 9(03)        VALUE ZERO.
009600     05  POS-COMMENT       PIC 9(03)        VALUE ZERO.
009700*---------------------------------------------------------------*
009800* OUT TABLE WORK
009900*---------------------------------------------------------------*
010000 01  OUT-TBL.
010100     05  FILLER          PIC X(03).
010200     05  OUT-LINE        PIC X(70).
010300     05  OUT-LINE-POS    REDEFINES OUT-LINE
010400                         OCCURS 70 TIMES
010500                         PIC X(01).
010600     05  OUT-SEQ         PIC X(08).
010700***
010800 77  OUT-IDX             PIC 999 VALUE ZERO.
010900 77  OUT-IDX-MAX         PIC 999 VALUE 70.
011000***
011100 77  IN-IDX              PIC 999 VALUE ZERO.
011200 77  IN-IDX2             PIC 999 VALUE ZERO.
011300 77  IN-IDX-MAX          PIC 999 VALUE 70.
011400***
011500 77  FH-IN-IDX-GET-XXX   PIC 999 VALUE ZERO.
011600*---------------------------------------------------------------*
011700* COMPARE TABLE WORK
011800*---------------------------------------------------------------*
011900 01  COMPARE-TBL.
012000     05  COMPARE-ITEM    PIC X(70).
012100     05  COMPARE-POS     REDEFINES COMPARE-ITEM
012200                         OCCURS 70 TIMES
012300                         PIC X(01).
012400***
012500 77  COMPARE-LEN         PIC 99 VALUE ZERO.
012600 77  COMPARE-IDX         PIC 999 VALUE ZERO.
012700 77  COMPARE-IDX-MAX     PIC 999 VALUE 70.
012800***
012900 77  MATCH-IDX           PIC 999 VALUE ZERO.
013000*---------------------------------------------------------------*
013100 01  AC-LINE               PIC 9(03)        VALUE 99.
013200***
013300 01  HL.
013400     05  FILLER            PIC X(01)        VALUE SPACES.
013500     05  FILLER            PIC X(45)        VALUE SPACES.
013600     05  FILLER            PIC X(40)        VALUE
013700         'XRB0005 CROSS REFERENCE'.
013800     05  FILLER            PIC X(45)        VALUE SPACES.
013900***
014000 01  DL.
014100     05  FILLER            PIC X(01)        VALUE SPACES.
014200     05  DL-X              PIC X(132)       VALUE SPACES.
014300*---------------------------------------------------------------*
014400 01  RH-X-REC.
014500     05 RH-JOB             PIC X(08)        VALUE SPACES.
014600     05 FILLER             PIC X(01)        VALUE SPACES.
014700     05 RH-JOB-STEP        PIC X(08)        VALUE SPACES.
014800     05 FILLER             PIC X(01)        VALUE SPACES.
014900     05 RH-PROC-PGM-FLAG   PIC X(04)        VALUE SPACES.
015000     05 FILLER             PIC X(01)        VALUE SPACES.
015100     05 RH-PROC-PGM        PIC X(08)        VALUE SPACES.
015200     05 FILLER             PIC X(01)        VALUE SPACES.
015300     05 RH-DDNAME          PIC X(17)        VALUE SPACES.
015400     05 FILLER             PIC X(01)        VALUE SPACES.
015500     05 RH-DSN-OR-SYSOUT   PIC X(50)        VALUE SPACES.
015600     05 FILLER             PIC X(01)        VALUE SPACES.
015700     05 RH-COMMENT         PIC X(70)        VALUE SPACES.
015800 EJECT
015900*===============================================================*
016000 PROCEDURE DIVISION.
016100*===============================================================*
016200 MAIN-PROGRAM.
016300 START-PROGRAM.
016400     OPEN INPUT IN-FILE.
016500     OPEN OUTPUT XREF-FILE
016600                 PRINT-FILE.
016700 MAIN-PROCESS.
016800     PERFORM 100-PARSE THRU
016900             100-PARSE-EXIT.
017000 FINISH-PROGRAM.
017100     MOVE AC-RECORDS TO DL-X.
017200     PERFORM 700-PRINT THRU
017300             700-PRINT-EXIT.
017400     CLOSE IN-FILE
017500           XREF-FILE
017600           PRINT-FILE.
017700     STOP RUN.
017800*---------------------------------------------------------------*
017900 100-PARSE.
018000 100-READ.
018100     READ IN-FILE.
018200     MOVE IN-CC TO CC-STATUS.
018300     IF CC-STATUS = CC-OK
018400         ADD 1 TO AC-IN
018500     ELSE
018600         IF CC-STATUS = CC-EOF
018700             GO TO 100-PARSE-EXIT
018800         ELSE
018900             DISPLAY 'ERROR,READ,IN,CC: ' CC-STATUS
019000             PERFORM 999-CANCEL THRU
019100                     999-CANCEL-EXIT.
019200 100-COMMENT-BYPASS.
019300     IF IN-SL-SL     = '//' AND
019400        IN-ASTER NOT = '*'
019500         NEXT SENTENCE
019600     ELSE
019700         GO TO 100-PARSE.
019800 100-JOB.
019900     MOVE ' JOB ' TO COMPARE-ITEM.
020000     MOVE  5      TO COMPARE-LEN.
020100     PERFORM 150-FIND THRU
020200             150-FIND-EXIT.
020300     MOVE MATCH-IDX TO POS-JOB.
020400     IF POS-JOB > 2 AND
020500        POS-JOB < 15
020600***      PRIOR NAKED EXEC STATEMENT PROCESS -
020700***      PRINT AN XREF RECORD FOR AN EXEC STMT THAT HAD NO SYSOUT/DSN
020800***      STATEMENTS (IE. SW-EXEC-NAKED-Y-N = 'Y').
020900         PERFORM 450-WRITE-XREF THRU
021000                 450-WRITE-XREF-EXIT
021100         MOVE 'N' TO SW-EXEC-NAKED-Y-N
021200***      INIT PROCESS *
021300         MOVE SPACES   TO RH-X-REC
021400         MOVE 'N'      TO SW-PROCESS-SYMBOLIC
021500***      GET JOB NAME *
021600         MOVE 1 TO IN-IDX
021700         PERFORM 200-GET-MOVE THRU
021800                 200-GET-MOVE-EXIT
021900         MOVE OUT-LINE TO RH-JOB
022000         GO TO 100-PARSE
022100     ELSE
022200         NEXT SENTENCE.
022300***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
022400***  EXEC PROCESS
022500***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
022600 100-EXEC-PROCESS.
022700     MOVE ' EXEC ' TO COMPARE-ITEM.
022800     MOVE  6       TO COMPARE-LEN.
022900     PERFORM 150-FIND THRU
023000             150-FIND-EXIT.
023100     MOVE MATCH-IDX TO POS-EXEC.
023200     IF POS-EXEC > 2 AND
023300        POS-EXEC < 23
023400***      PRIOR NAKED EXEC STATEMENT PROCESS -
023500***      PRINT AN XREF RECORD FOR AN EXEC STMT THAT HAD NO SYSOUT/DSN
023600***      STATEMENTS (IE. SW-EXEC-NAKED-Y-N = 'Y').
023700         PERFORM 450-WRITE-XREF THRU
023800                 450-WRITE-XREF-EXIT
023900         MOVE 'Y' TO SW-EXEC-NAKED-Y-N
024000***      INIT PROCESS *
024100         MOVE SPACE TO RH-DDNAME
024200         MOVE ZERO  TO POS-DD
024300***      GET JOB STEP *
024400         MOVE 1 TO IN-IDX
024500         PERFORM 200-GET-MOVE THRU
024600                 200-GET-MOVE-EXIT
024700         MOVE OUT-LINE TO RH-JOB-STEP
024800     ELSE
024900         GO TO 100-DDNAME.
025000***
025100 100-EXEC-PGM.
025200     MOVE ' PGM=' TO COMPARE-ITEM.
025300     MOVE  5       TO COMPARE-LEN.
025400     PERFORM 150-FIND THRU
025500             150-FIND-EXIT.
025600     MOVE MATCH-IDX TO POS-PGM.
025700     IF POS-PGM  > 7 AND
025800        POS-PGM  < 28
025900***      INIT PROCESS *
026000         MOVE 'N'   TO SW-PROCESS-SYMBOLIC
026100         MOVE 'PGM' TO RH-PROC-PGM-FLAG
026200***      GET PROGRAM NAME *
026300         COMPUTE IN-IDX = POS-PGM + 5
026400         PERFORM 200-GET-MOVE THRU
026500                 200-GET-MOVE-EXIT
026600         MOVE OUT-LINE   TO RH-PROC-PGM
026700         GO TO 100-PARSE
026800     ELSE
026900         NEXT SENTENCE.
027000 100-EXEC-PROC.
027100***  INIT PROCESS FOR PROC *
027200     MOVE 'Y'    TO SW-PROCESS-SYMBOLIC.
027300     MOVE 'PROC' TO RH-PROC-PGM-FLAG.
027400***  COUNT LEADING SPACES PRECEDING PROC NAME *
027500     COMPUTE IN-IDX = POS-EXEC + 5.
027600     PERFORM 250-GET-COMMENT THRU
027700             250-GET-COMMENT-EXIT.
027800     MOVE OUT-LINE TO RH-COMMENT.
027900     MOVE ZERO     TO POS-SPACES.
028000     INSPECT RH-COMMENT TALLYING POS-SPACES FOR LEADING SPACE.
028100***  GET PROC NAME *
028200     COMPUTE IN-IDX = POS-EXEC + 5 + POS-SPACES.
028300     PERFORM 200-GET-MOVE THRU
028400             200-GET-MOVE-EXIT.
028500     MOVE OUT-LINE   TO RH-PROC-PGM.
028600     COMPUTE IN-IDX = FH-IN-IDX-GET-XXX + 1.
028700     IF IN-LINE-POS  (IN-IDX) = ','
028800***      GET SYMBOLICS, COND CODES, PARMS, OR COMMENTS *
028900         MOVE 'Y'   TO SW-PROCESS-SYMBOLIC
029000         COMPUTE IN-IDX, POS-COMMENT = IN-IDX + 1
029100         PERFORM 225-COMMENT-OR-SYMBOLIC THRU
029200                 225-COMMENT-OR-SYMBOLIC-EXIT
029300***      WRITE SYMBOLICS CROSS REFERENCE RECORD *
029400         PERFORM 500-WRITE-XREF THRU
029500                 500-WRITE-XREF-EXIT
029510         MOVE SPACE TO RH-COMMENT
029600     ELSE
029700         NEXT SENTENCE.
029800 100-EXEC-END.
029900     GO TO 100-PARSE.
030000 100-EXEC-PROCESS-END.
030100***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
030200***  DDNAME PROCESS
030300***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
030400 100-DDNAME.
030500     IF IN-SL-SL     = '//' AND
030600        IN-ASTER NOT = SPACE
030700***      INIT PROCESS - TURN SYMBOLICS SWITCH OFF *
030800         MOVE 'N' TO SW-PROCESS-SYMBOLIC
030900***      GET DDNAME *
031000         MOVE 1 TO IN-IDX
031100         PERFORM 200-GET-MOVE THRU
031200                 200-GET-MOVE-EXIT
031300         MOVE OUT-LINE TO RH-DDNAME
031400***      THERE MUST BE A ' DD ' IN THIS STATEMENT *
031500         MOVE ' DD ' TO COMPARE-ITEM
031600         MOVE  4     TO COMPARE-LEN
031700         PERFORM 150-FIND THRU
031800                 150-FIND-EXIT
031900         MOVE MATCH-IDX TO POS-DD
032000     ELSE
032100         NEXT SENTENCE.
032200***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
032300***  SYMBOLICS PROCESS
032400***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
032500 100-SYMBOLIC.
032600     IF SW-PROCESS-SYMBOLIC = 'Y'
032700         MOVE ZERO TO POS-SPACES
032800         INSPECT IN-LINE TALLYING POS-SPACES FOR LEADING SPACE
032900         COMPUTE IN-IDX = POS-SPACES + 1
033000         PERFORM 250-GET-COMMENT THRU
033100                 250-GET-COMMENT-EXIT
033200         MOVE OUT-LINE TO RH-COMMENT
033300         PERFORM 500-WRITE-XREF THRU
033400                 500-WRITE-XREF-EXIT
033410         MOVE SPACES TO RH-COMMENT
033500         GO TO 100-PARSE
033600     ELSE
033700         NEXT SENTENCE.
033800***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
033900***  DATA SET NAME AND SYSOUT PROCESS
034000***-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   *
034100 100-DSN-OR-SYSOUT-PROCESS.
034200 100-DD.
034300     IF POS-DD > ZERO
034400***      CONTINUE PROCESS FOR VALID DD'S ONLY *
034500         NEXT SENTENCE
034600     ELSE
034700         GO TO 100-PARSE.
034800 100-DSN.
034900     MOVE ' DSN=' TO COMPARE-ITEM.
035000     MOVE  5      TO COMPARE-LEN.
035100     PERFORM 150-FIND THRU
035200             150-FIND-EXIT.
035300     MOVE MATCH-IDX TO POS-DSN.
035400     IF POS-DSN > 2 AND
035500        POS-DSN < 53
035600         COMPUTE IN-IDX = POS-DSN + 5
035700         PERFORM 115-DSN-OR-SYSOUT THRU
035800                 115-DSN-OR-SYSOUT-EXIT
035900     ELSE
036000         NEXT SENTENCE.
036100 100-SYSOUT.
036200     MOVE ' SYSOUT=' TO COMPARE-ITEM.
036300     MOVE  8         TO COMPARE-LEN.
036400     PERFORM 150-FIND THRU
036500             150-FIND-EXIT.
036600     COMPUTE POS-SYSOUT = MATCH-IDX + 1.
036700***
036800     IF POS-SYSOUT > 5 AND
036900        POS-SYSOUT < 56
037000         MOVE POS-SYSOUT TO IN-IDX
037100         PERFORM 115-DSN-OR-SYSOUT THRU
037200                 115-DSN-OR-SYSOUT-EXIT
037300     ELSE
037400         NEXT SENTENCE.
037500***
037600 100-REITERATE.
037700     GO TO 100-PARSE.
037800 100-PARSE-EXIT. EXIT.
037900*---------------------------------------------------------------*
038000* WRITE A DSN OR SYSOUT XREF RECORD
038100*
038200* NOTE: IN-IDX POINTS TO THE START OF A DATA SET NAME, OR THE
038300* START OF A SYSOUT STATEMENT.  EXAMPLES:
038400*
038500*    DSN=SAMPLE,              SYSOUT=J
038600*        |                    |
038700*        +-> IN-IDX           +-> IN-IDX
038800*---------------------------------------------------------------*
038900 115-DSN-OR-SYSOUT.
039000***  GET DSN OR SYSOUT FIELDS *
039100     PERFORM 200-GET-MOVE THRU
039200             200-GET-MOVE-EXIT.
039300     MOVE OUT-LINE TO RH-DSN-OR-SYSOUT.
039400 115-FIND-COMMENT.
039500***  FIND THE START OF THE DSN OR SYSOUT COMMENT FIELD *
039600     MOVE ' '                TO COMPARE-ITEM.
039700     MOVE  1                 TO COMPARE-LEN.
039800     MOVE  FH-IN-IDX-GET-XXX TO IN-IDX.
039900     PERFORM 150-FIND-EP2 THRU
040000             150-FIND-EXIT.
040100     MOVE MATCH-IDX TO POS-COMMENT
040200                       IN-IDX.
040300***  MOVE COMMENT TO RH-COMMENT *
040400     PERFORM 225-COMMENT-OR-SYMBOLIC THRU
040500             225-COMMENT-OR-SYMBOLIC-EXIT.
040600***  FLAG THE LAST EXEC STATEMENT AS A NON NAKED EXEC *
040700     MOVE 'N' TO SW-EXEC-NAKED-Y-N.
040800***  WRITE DSN OR SYSOUT CROSS REFERENCE RECORD *
040900     PERFORM 500-WRITE-XREF THRU
041000             500-WRITE-XREF-EXIT.
041100 115-INIT-DSN-OR-SYSOUT-COMMENT.
041200 115-INIT-COMMENT.
041300***  DDNAME IS NOT INITIALIZED HERE SINCE THERE MAY BE MULTIPLE /
041400***  CONCATENATED DSN'S PER DDNAME *
041500     MOVE SPACE TO RH-DSN-OR-SYSOUT
041600                   RH-COMMENT.
041700 115-DSN-OR-SYSOUT-EXIT. EXIT.
041800*---------------------------------------------------------------*
041900* FIND COMPARE-ITEM WITHIN IN-LINE. IF MATCH, MATCH-IDX WILL
042000* POINT TO STARTING POSITION OF COMPARE-ITEM WITHIN IN-LINE.
042100* IF NO MATCH, MATCH-IDX WILL BE SET TO ZERO.
042200*
042300* ENTRY POINTS TO THIS ROUTINE ARE 150-FIND, AND 150-FIND-EP2.
042400* IF A STARTING LOCATION IS KNOWN, THE '150-FIND-EP2' ENTRY
042500* IS USED.
042600*---------------------------------------------------------------*
042700 150-FIND.
042800     MOVE 1    TO IN-IDX.
042900 150-FIND-EP2.
043000     MOVE ZERO TO MATCH-IDX.
043100 150-LOOP.
043200     IF IN-LINE-POS (IN-IDX) = COMPARE-POS (1)
043300         PERFORM 175-FIND THRU
043400                 175-FIND-EXIT
043500         IF MATCH-IDX > ZERO
043600***          WE HAD A MATCH - EXIT *
043700             GO TO 150-FIND-EXIT
043800         ELSE
043900             NEXT SENTENCE
044000     ELSE
044100         NEXT SENTENCE.
044200 150-REITERATE.
044300     COMPUTE IN-IDX = IN-IDX + 1
044400     IF IN-IDX > IN-IDX-MAX
044500         NEXT SENTENCE
044600     ELSE
044700         GO TO 150-LOOP.
044800 150-FIND-EXIT. EXIT.
044900*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
045000* IF WE HAVE A MATCH, THE POSITION IN-IDX PASSED BY 150-FIND
045100* WILL BE SAVED IN MATCH-IDX.
045200*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
045300 175-FIND.
045400 175-FIND-LEN-EQ-1.
045500     IF COMPARE-LEN = 1
045600***      WE HAVE A MATCH - LET'S MARK POSITION *
045700         MOVE IN-IDX TO MATCH-IDX
045800         GO TO 175-FIND-EXIT
045900     ELSE
046000         NEXT SENTENCE.
046100 175-FIND-LEN-GT-1.
046200     COMPUTE IN-IDX2 = IN-IDX + 1.
046300     MOVE 2 TO COMPARE-IDX.
046400 175-LOOP.
046500     IF IN-LINE-POS (IN-IDX2) = COMPARE-POS (COMPARE-IDX)
046600         IF COMPARE-IDX = COMPARE-LEN
046700             MOVE IN-IDX TO MATCH-IDX
046800             GO TO 175-FIND-EXIT
046900         ELSE
047000             COMPUTE IN-IDX2     = IN-IDX2 + 1
047100             COMPUTE COMPARE-IDX = COMPARE-IDX + 1
047200             IF IN-IDX2 > IN-IDX-MAX
047300                 NEXT SENTENCE
047400             ELSE
047500                 GO TO 175-LOOP
047600     ELSE
047700         NEXT SENTENCE.
047800 175-FIND-EXIT. EXIT.
047900*---------------------------------------------------------------*
048000* GET AND MOVE AN ITEM
048100*---------------------------------------------------------------*
048200 200-GET-MOVE.
048300     MOVE 1      TO OUT-IDX.
048400     MOVE SPACES TO OUT-LINE.
048500     MOVE ZERO   TO AC-LEFT-PAREN
048600                    AC-RIGHT-PAREN.
048700     MOVE IN-IDX TO FH-IN-IDX-GET-XXX.
048800 200-LOOP.
048900 200-CHECK-IF-DONE.
049000     IF IN-IDX > IN-IDX-MAX
049100         GO TO 200-GET-MOVE-EXIT
049200     ELSE
049300         IF IN-LINE-POS(IN-IDX) = ' '
049400             GO TO 200-GET-MOVE-EXIT
049500         ELSE
049600             IF IN-LINE-POS(IN-IDX) = ',' AND
049700                AC-LEFT-PAREN       = AC-RIGHT-PAREN
049800                 GO TO 200-GET-MOVE-EXIT
049900             ELSE
050000                 NEXT SENTENCE.
050100 200-MOVE.
050200     MOVE IN-LINE-POS (IN-IDX) TO OUT-LINE-POS (OUT-IDX).
050300     MOVE IN-IDX               TO FH-IN-IDX-GET-XXX.
050400     IF IN-LINE-POS (IN-IDX) = '('
050500         COMPUTE AC-LEFT-PAREN = AC-LEFT-PAREN + 1
050600     ELSE
050700         IF IN-LINE-POS (IN-IDX) = ')'
050800             COMPUTE AC-RIGHT-PAREN = AC-RIGHT-PAREN + 1
050900         ELSE
051000             NEXT SENTENCE.
051100 200-INCREMENT.
051200     ADD 1 TO IN-IDX
051300              OUT-IDX.
051400 200-REITERATE.
051500     GO TO 200-LOOP.
051600 200-GET-MOVE-EXIT. EXIT.
051700*---------------------------------------------------------------*
051800* FIND COMMENT OR SYMBOLIC AND MOVE TO RH-COMMENT
051900* (LEADING BLANKS ARE STRIPPED OFF).
052000*
052100* NOTE: UPON ENTRY IN-IDX AND POS-COMMENT POINT TO THE START
052200* OF THE COMMENT/SYMBOLIC FIELD.  EXAMPLE:
052300* ....
052400*  DSN=SAMPLE,DISP=SHR    DUMB COMMENT
052500*                     |
052600*                     +--> IN-IDX AND POS-COMMENT
052700*---------------------------------------------------------------*
052800 225-COMMENT-OR-SYMBOLIC.
052900***  GET COMMENT OR SYMBOLICS *
053000     PERFORM 250-GET-COMMENT THRU
053100             250-GET-COMMENT-EXIT.
053200     MOVE OUT-LINE TO RH-COMMENT.
053300***  ELIMINATE LEADING SPACES FROM COMMENT *
053400     MOVE ZERO     TO POS-SPACES.
053500     INSPECT RH-COMMENT TALLYING POS-SPACES FOR LEADING SPACE.
053600     COMPUTE IN-IDX = POS-COMMENT + POS-SPACES.
053700     IF IN-IDX > IN-IDX-MAX
053800         MOVE SPACES TO RH-COMMENT
053900     ELSE
054000         PERFORM 250-GET-COMMENT THRU
054100                 250-GET-COMMENT-EXIT
054200         MOVE OUT-LINE TO RH-COMMENT.
054300 225-COMMENT-OR-SYMBOLIC-EXIT. EXIT.
054400*---------------------------------------------------------------*
054500* GET A COMMENT
054600*---------------------------------------------------------------*
054700 250-GET-COMMENT.
054800     MOVE 1      TO OUT-IDX.
054900     MOVE SPACES TO OUT-LINE.
055000     MOVE IN-IDX TO FH-IN-IDX-GET-XXX.
055100 250-MOVE.
055200     IF IN-IDX > IN-IDX-MAX
055300         GO TO 250-GET-COMMENT-EXIT
055400     ELSE
055500         MOVE IN-LINE-POS (IN-IDX) TO OUT-LINE-POS (OUT-IDX)
055600         MOVE IN-IDX               TO FH-IN-IDX-GET-XXX.
055700 250-INCREMENT.
055800     ADD 1 TO IN-IDX
055900              OUT-IDX.
056000 250-REITERATE.
056100     GO TO 250-MOVE.
056200 250-GET-COMMENT-EXIT. EXIT.
056300*---------------------------------------------------------------*
056400* WRITE A NAKED EXEC CROSS REFERENCE RECORD (IE. NO ASSOCIATED
056500* DSN/SYSOUT STATEMENTS).
056600*---------------------------------------------------------------*
056700 450-WRITE-XREF.
056800     IF SW-EXEC-NAKED-Y-N = 'Y'
056900         PERFORM 500-WRITE-XREF THRU
057000                 500-WRITE-XREF-EXIT
057100     ELSE
057200         NEXT SENTENCE.
057300 450-WRITE-XREF-EXIT. EXIT.
057400*---------------------------------------------------------------*
057500* WRITE CROSS REFERENCE FILE (XREF)
057600*---------------------------------------------------------------*
057700 500-WRITE-XREF.
057800     MOVE RH-X-REC TO X-REC.
057900     WRITE X-REC.
058000     MOVE XREF-CC TO CC-STATUS.
058100     IF CC-STATUS = CC-OK
058200         ADD 1 TO AC-XREF
058300     ELSE
058400         DISPLAY 'ERROR,WRITE,XREF,CC: ' CC-STATUS
058500         PERFORM 999-CANCEL THRU
058600                 999-CANCEL-EXIT.
058700 500-WRITE-XREF-EXIT. EXIT.
058800*---------------------------------------------------------------*
058900* PRINT REPORT
059000*---------------------------------------------------------------*
059100 700-PRINT.
059200     ADD 1 TO AC-LINE.
059300     IF AC-LINE > 60
059400         WRITE PRINT-REC FROM HL AFTER ADVANCING TOP-OF-PAGE
059500         WRITE PRINT-REC FROM DL AFTER ADVANCING 2 LINES
059600         MOVE 3 TO AC-LINE
059700     ELSE
059800         WRITE PRINT-REC FROM DL.
059900 700-PRINT-EXIT. EXIT.
060000*---------------------------------------------------------------*
060100* CANCEL ROUTINE
060200*---------------------------------------------------------------*
060300 999-CANCEL.
060400     DISPLAY 'XRB0005,ABNORMAL END * * * * * * * *'.
060500     MOVE +16 TO RETURN-CODE.
060600     STOP RUN.
060700 999-CANCEL-EXIT. EXIT.

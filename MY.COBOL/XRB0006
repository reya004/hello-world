000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. XRB0006.
000300 AUTHOR. TONY REY.
000400 INSTALLATION. TONY REY INC.
000500 DATE-WRITTEN. 11/05/91.
000600 DATE-COMPILED.
000700*================================================================*
000800* PROGRAM NARRATIVE
000900*================================================================*
001000* PRODUCE THE SYMBOLICS DEFAULT FILE FROM THE PROCEDURE XREF.
001100*================================================================*
001200* MODIFICATION LOG
001300*
001400* TWO PROGRAMMER INITIALS IMMEDIATELY FOLLOWED BY THE CHANGE
001500* DATE IN COLUMNS 73-80 SHOULD MARK ANY PROGRAM CHANGES.
001600*================================================================*
001700* MM/DD/YY  PROGRAMMER  PROBLEM/RFS NUMBER: DESCRIPTION
001800* --------  ----------  ------------------------------------------
001900* 08/12/92  TONY REY    I WROTE THIS PROGRAM.
002000*================================================================*
002100 ENVIRONMENT DIVISION.
002200 CONFIGURATION SECTION.
002300 SOURCE-COMPUTER.  IBM-370.
002400 OBJECT-COMPUTER.  IBM-370.
002500 SPECIAL-NAMES. C01 IS TOP-OF-PAGE.
002600 INPUT-OUTPUT SECTION.
002700 FILE-CONTROL.
002800     SELECT XREF-PROC            ASSIGN TO UT-S-XR0061I
002900                                 FILE STATUS IS XREF-PROC-CC.
003000     SELECT SYMBOLICS            ASSIGN TO DA-XR0061O
003100                                 ORGANIZATION IS INDEXED
003200                                 ACCESS IS DYNAMIC
003300                                 RECORD KEY  IS SYM-KEY
003400                                 FILE STATUS IS SYMBOLICS-CC.
003500 DATA DIVISION.
003600 FILE SECTION.
003700*---------------------------------------------------------------*
003800 FD  XREF-PROC
003900     BLOCK CONTAINS 0 RECORDS
004000     RECORDING MODE IS F
004100     LABEL RECORDS ARE STANDARD.
004200 01  X-PROC-REC.
004300     05 X-PROC                  PIC X(08).
004400     05 FILLER                  PIC X(01).
004500     05 X-PROC-STEP             PIC X(08).
004600     05 FILLER                  PIC X(01).
004700     05 X-PROC-PGM              PIC X(08).
004800     05 FILLER                  PIC X(01).
004900     05 X-PROC-DDNAME           PIC X(08).
005000     05 FILLER                  PIC X(01).
005100     05 X-PROC-DSN-OR-SYSOUT    PIC X(50).
005200     05 FILLER                  PIC X(01).
005300     05 X-PROC-COMMENT          PIC X(70).
005400*---------------------------------------------------------------*
005500 FD  SYMBOLICS
005600     DATA RECORD IS SYM-REC.
005700 01  SYM-REC.
005800     05 SYM-KEY.
005900        10 SYM-PROC             PIC X(08).
006000        10 SYM-SYMBOLIC         PIC X(07).
006100     05 SYM-VALUE               PIC X(40).
006200 EJECT
006300*===============================================================*
006400 WORKING-STORAGE SECTION.
006500*===============================================================*
006600 COPY CCFILE.
006700 77  XREF-PROC-CC          PIC XX VALUE SPACES.
006800 77  SYMBOLICS-CC          PIC XX VALUE SPACES.
006900*---------------------------------------------------------------*
007000 77  AC-LEFT-QUOTE         PIC 99           VALUE ZERO.
007100 77  AC-RIGHT-QUOTE        PIC 99           VALUE ZERO.
007200*---------------------------------------------------------------*
007300 77  AC-XREF-PROC          PIC 9(7)         VALUE ZERO.
007400 77  AC-SYMBOLICS          PIC 9(7)         VALUE ZERO.
007500*---------------------------------------------------------------*
007600 77  CSN-ZERO              PIC 9            VALUE ZERO.
007700 77  CSA-COMMA             PIC X            VALUE ','.
007800 77  CSA-EQUAL             PIC X            VALUE '='.
007900 01  CSA-QUOTE-VIA-BRUTE-FORCE.
008000     05 CSN-125            PIC S9(4) COMP   VALUE +125.
008100     05 CSN-125-R          REDEFINES CSN-125.
008200        10 FILLER          PIC X.
008300        10 CSA-QUOTE       PIC X.
008400*---------------------------------------------------------------*
008500 77  POS-SCAN              PIC 9(03)        VALUE ZERO.
008600 77  POS-ANCHOR            PIC 9(03)        VALUE ZERO.
008700 77  SCAN-SIZE             PIC 9(03)        VALUE ZERO.
008800 77  WS-PHASE              PIC 9            VALUE ZERO.
008900 77  SYM-LEN               PIC 9(03)        VALUE ZERO.
009000*---------------------------------------------------------------*
009100 EJECT
009200*===============================================================*
009300 PROCEDURE DIVISION.
009400*===============================================================*
009500 MAIN-PROGRAM.
009600     DISPLAY 'XRB0006 STARTING'.
009700 OPEN-FILES.
009800     DISPLAY 'OPENING FILES'.
009900***
010000     OPEN INPUT XREF-PROC.
010100     MOVE XREF-PROC-CC TO CC-STATUS.
010200     IF CC-STATUS NOT = CC-OK
010300         DISPLAY 'ERROR,OPEN,XREF-PROC,CC: ' CC-STATUS
010400         PERFORM 999-CANCEL THRU
010500                 999-CANCEL-EXIT
010600     END-IF.
010700***
010800     OPEN OUTPUT SYMBOLICS.
010900     MOVE SYMBOLICS-CC TO CC-STATUS.
011000     IF CC-STATUS NOT = CC-OK
011100         DISPLAY 'ERROR,OPEN,SYMBOLICS,CC: ' CC-STATUS
011200         PERFORM 999-CANCEL THRU
011300                 999-CANCEL-EXIT
011400     END-IF.
011500 INIT-READ.
011600     DISPLAY 'INITIAL READ'.
011700     PERFORM 400-READ THRU
011800             400-READ-EXIT.
011900 MAIN-PROCESS.
012000     DISPLAY 'MAIN PROCESS'.
012100     PERFORM 100-MAIN-PROCESS THRU
012200             100-MAIN-PROCESS-EXIT
012300             UNTIL XREF-PROC-CC = CC-EOF.
012400 CLOSE-FILES.
012500     DISPLAY 'CLOSE FILES'.
012600***
012700     CLOSE XREF-PROC.
012800     MOVE XREF-PROC-CC TO CC-STATUS.
012900     IF CC-STATUS NOT = CC-OK
013000         DISPLAY 'ERROR,CLOSE,XREF-PROC,CC: ' CC-STATUS
013100         PERFORM 999-CANCEL THRU
013200                 999-CANCEL-EXIT
013300     END-IF.
013400***
013500     CLOSE SYMBOLICS.
013600     MOVE SYMBOLICS-CC TO CC-STATUS.
013700     IF CC-STATUS NOT = CC-OK
013800         DISPLAY 'ERROR,CLOSE,SYMBOLICS,CC: ' CC-STATUS
013900         PERFORM 999-CANCEL THRU
014000                 999-CANCEL-EXIT
014100     END-IF.
014200 100-FINISH-PROGRAM.
014300     PERFORM 900-STATISTICS THRU
014400             900-STATISTICS-EXIT.
014500     DISPLAY 'XRB0006 ENDING'.
014600     STOP RUN.
014700*---------------------------------------------------------------*
014800 100-MAIN-PROCESS.
014900     IF X-PROC-STEP           = SPACES AND
015000        X-PROC-PGM            = SPACES AND
015100        X-PROC-DDNAME         = SPACES AND
015200        X-PROC-DSN-OR-SYSOUT  = SPACES AND
015300        X-PROC-COMMENT    NOT = SPACES
015400***      START PROCESSING THE SYBOLIC *
015500         PERFORM 110-PARSE THRU
015600                 110-PARSE-EXIT
015700     END-IF.
015800 100-READ.
015900     PERFORM 400-READ THRU
016000             400-READ-EXIT.
016100 100-MAIN-PROCESS-EXIT. EXIT.
016200*---------------------------------------------------------------*
016300* PARSE THE COMMENT FIELD FOR SYMBOLICS
016400*---------------------------------------------------------------*
016500 110-PARSE.
016600     MOVE ZERO                     TO WS-PHASE.
016700     MOVE 1                        TO POS-ANCHOR.
016800     MOVE 1                        TO POS-SCAN.
016900     MOVE LENGTH OF X-PROC-COMMENT TO SCAN-SIZE.
017000 110-PARSE-LOOP.
017100     IF WS-PHASE = ZERO
017200         IF X-PROC-COMMENT(POS-SCAN: 1) = SPACE
017300             GO TO 110-PARSE-EXIT
017400         ELSE
017500             MOVE 1        TO WS-PHASE
017600             MOVE POS-SCAN TO POS-ANCHOR
017700             MOVE ZERO     TO AC-LEFT-QUOTE
017800             MOVE ZERO     TO AC-RIGHT-QUOTE
017900             MOVE SPACES   TO SYM-REC
018000             MOVE X-PROC   TO SYM-PROC
018100             GO TO 110-PHASE-1
018200         END-IF
018300     END-IF.
018400 110-PHASE-2.
018500     IF WS-PHASE = 2
018600         IF X-PROC-COMMENT(POS-SCAN: 1) = SPACE OR
018700            X-PROC-COMMENT(POS-SCAN: 1) = CSA-COMMA
018800             MOVE SPACES TO SYM-VALUE
018900             PERFORM 500-WRITE-SYMBOLICS THRU
019000                     500-WRITE-SYMBOLICS-EXIT
019100             IF X-PROC-COMMENT(POS-SCAN: 1) = SPACE
019200                 GO TO 110-PARSE-EXIT
019300             ELSE
019400                 MOVE ZERO TO WS-PHASE
019500                 GO TO 110-CONTINUE
019600             END-IF
019700         ELSE
019800             MOVE 3 TO WS-PHASE
019900             IF X-PROC-COMMENT (POS-SCAN: 1) = CSA-QUOTE
020000                 ADD 1 TO AC-LEFT-QUOTE
020100                 COMPUTE POS-ANCHOR = POS-SCAN + 1
020200                 GO TO 110-CONTINUE
020300             ELSE
020400                 MOVE POS-SCAN TO POS-ANCHOR
020500                 GO TO 110-PHASE-3
020600             END-IF
020700         END-IF
020800     END-IF.
020900 110-PHASE-4.
021000     IF WS-PHASE = 4
021100         IF X-PROC-COMMENT(POS-SCAN: 1) = SPACE
021200             GO TO 110-PARSE-EXIT
021300         ELSE
021400             IF X-PROC-COMMENT(POS-SCAN: 1) = CSA-COMMA
021500                 MOVE ZERO TO WS-PHASE
021600                 GO TO 110-CONTINUE
021700             ELSE
021800                 GO TO 110-PARSE-ERROR
021900             END-IF
022000         END-IF
022100     END-IF.
022200 110-PHASE-1.
022300     IF WS-PHASE = 1
022400         IF X-PROC-COMMENT (POS-SCAN: 1) = CSA-EQUAL
022500             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
022600             IF SYM-LEN > ZERO AND
022700                SYM-LEN < 8
022800                 MOVE X-PROC-COMMENT (POS-ANCHOR: SYM-LEN)
022900                   TO SYM-SYMBOLIC
023000                 MOVE 2 TO WS-PHASE
023100                 GO TO 110-CONTINUE
023200             ELSE
023300                 GO TO 110-PARSE-ERROR
023400             END-IF
023500         END-IF
023600     END-IF.
023700 110-PHASE-3.
023800     IF WS-PHASE = 3
023900         IF X-PROC-COMMENT(POS-SCAN: 1) = CSA-QUOTE
024000             COMPUTE AC-RIGHT-QUOTE = AC-RIGHT-QUOTE + 1
024100         END-IF
024200***
024300         IF X-PROC-COMMENT (POS-SCAN: 1) = SPACE AND
024400            AC-LEFT-QUOTE                = AC-RIGHT-QUOTE
024500             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
024600             IF SYM-LEN = ZERO
024700                 MOVE SPACES TO SYM-VALUE
024800             ELSE
024900                 MOVE X-PROC-COMMENT (POS-ANCHOR: SYM-LEN)
025000                   TO SYM-VALUE
025100             END-IF
025200             PERFORM 500-WRITE-SYMBOLICS THRU
025300                     500-WRITE-SYMBOLICS-EXIT
025400             GO TO 110-PARSE-EXIT
025500         END-IF
025600***
025700         IF X-PROC-COMMENT(POS-SCAN: 1) = CSA-QUOTE AND
025800            AC-LEFT-QUOTE               = AC-RIGHT-QUOTE
025900             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
026000             IF SYM-LEN = ZERO
026100                 MOVE SPACES TO SYM-VALUE
026200             ELSE
026300                 MOVE X-PROC-COMMENT (POS-ANCHOR: SYM-LEN)
026400                   TO SYM-VALUE
026500             END-IF
026600             PERFORM 500-WRITE-SYMBOLICS THRU
026700                     500-WRITE-SYMBOLICS-EXIT
026800             MOVE 4 TO WS-PHASE
026900         END-IF
027000***
027100         IF X-PROC-COMMENT(POS-SCAN: 1) = CSA-COMMA AND
027200            AC-LEFT-QUOTE               = AC-RIGHT-QUOTE
027300             COMPUTE SYM-LEN = POS-SCAN - POS-ANCHOR
027400             IF SYM-LEN = ZERO
027500                 MOVE SPACES TO SYM-VALUE
027600             ELSE
027700                 MOVE X-PROC-COMMENT (POS-ANCHOR: SYM-LEN)
027800                   TO SYM-VALUE
027900             END-IF
028000             PERFORM 500-WRITE-SYMBOLICS THRU
028100                     500-WRITE-SYMBOLICS-EXIT
028200             MOVE ZERO TO WS-PHASE
028300         END-IF
028400     END-IF.
028500 110-CONTINUE.
028600     IF POS-SCAN < SCAN-SIZE
028700         COMPUTE POS-SCAN = POS-SCAN + 1
028800         GO TO 110-PARSE-LOOP
028900     ELSE
029000         GO TO 110-PARSE-EXIT
029100     END-IF.
029200 110-PARSE-ERROR.
029300     DISPLAY 'ERROR,DATA,XREF-PROC,REC: ' AC-XREF-PROC.
029400     DISPLAY '->' X-PROC-REC.
029500 110-PARSE-EXIT. EXIT.
029600*---------------------------------------------------------------*
029700* READ THE PROCEDURE XREF
029800*---------------------------------------------------------------*
029900 400-READ.
030000     READ XREF-PROC.
030100     MOVE XREF-PROC-CC TO CC-STATUS.
030200     IF CC-STATUS = CC-OK
030300         ADD 1 TO AC-XREF-PROC
030400     ELSE
030500         IF CC-STATUS = CC-EOF
030600             GO TO 400-READ-EXIT
030700         ELSE
030800             DISPLAY 'ERROR,READ,XREF-PROC,CC: ' CC-STATUS
030900             PERFORM 999-CANCEL THRU
031000                     999-CANCEL-EXIT
031100         END-IF
031200     END-IF.
031300 400-READ-EXIT. EXIT.
031400*---------------------------------------------------------------*
031500* WRITE SYMBOLICS
031600*---------------------------------------------------------------*
031700 500-WRITE-SYMBOLICS.
031800     WRITE SYM-REC.
031900     MOVE SYMBOLICS-CC TO CC-STATUS.
032000     IF CC-STATUS = CC-OK
032100         ADD 1 TO AC-SYMBOLICS
032200     ELSE
032300         IF CC-STATUS = CC-DUP-KEY
032400             DISPLAY 'ERROR,DATA,XREF-PROC,REC: ' AC-XREF-PROC
032500             DISPLAY 'ERROR,DUP KEY,SYM-REC: '  SYM-REC
032600         ELSE
032700             DISPLAY 'ERROR,WRITE,SYMBOLICS,CC: ' CC-STATUS
032800             PERFORM 999-CANCEL THRU
032900                     999-CANCEL-EXIT
033000         END-IF
033100     END-IF.
033200 500-WRITE-SYMBOLICS-EXIT. EXIT.
033300*---------------------------------------------------------------*
033400* PRINT REPORT
033500*---------------------------------------------------------------*
033600 900-STATISTICS.
033700     DISPLAY 'SYMBOLICS WRITTEN: ' AC-SYMBOLICS.
033800     DISPLAY 'XREF-PROC READ   : ' AC-XREF-PROC.
033900 900-STATISTICS-EXIT. EXIT.
034000*---------------------------------------------------------------*
034100* CANCEL ROUTINE
034200*---------------------------------------------------------------*
034300 999-CANCEL.
034400     DISPLAY 'XRB0006,ABNORMAL END * * * * * * * *'.
034500     PERFORM 900-STATISTICS THRU
034600             900-STATISTICS-EXIT.
034700     COMPUTE CSN-ZERO = CSN-ZERO / CSN-ZERO.
034800 999-CANCEL-EXIT. EXIT.
